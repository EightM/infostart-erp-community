Процедура ПередЗаписью(Отказ)

	Если НЕ ОбменДанными.Загрузка И НЕ ЭтоГруппа И НЕ ЭтоНовый() И ВидСклада <> Ссылка.ВидСклада И СуществуютСсылки() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Существуют документы, в которых выбран склад """ + Наименование + """.
		                |Реквизит ""Вид склада"" не может быть изменен, элемент не записан.");

		Отказ = Истина;
	КонецЕсли;

	// не дадим записать розничный склад без указания типа цен
	Если НЕ ОбменДанными.Загрузка И Не ЭтоГруппа И (ВидСклада = Перечисления.ВидыСкладов.Розничный) И НЕ ЗначениеЗаполнено(ТипЦенРозничнойТорговли) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Для склада вида """+ВидСклада+""" необходимо указать тип цен розничной торговли, элемент не записан.");

		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

Функция СуществуютСсылки() Экспорт
	Запрос=Новый Запрос();
    Запрос.УстановитьПараметр("Склад", Ссылка);
	Запрос.Текст="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	ИсточникДанных.Регистратор КАК Документ
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах Как ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Склад = &Склад
	|";
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

