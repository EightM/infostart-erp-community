&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	СистемнаяИнформация = Новый СистемнаяИнформация();
	ИдентификаторКлиента = ВРег(СистемнаяИнформация.ИдентификаторКлиента);
	
	РабочееМесто=ПолучитьРабочееМестоПоИдентификаторуКлиента(ИдентификаторКлиента);
	Если НЕ РабочееМесто=Неопределено Тогда		
		Отказ = Истина;
			                  
		Текст = НСтр("ru='Создание нового рабочего места не требуется. 
					|Для данного идентификатора клиента оно уже создано.
					|Открыть существующее рабочее место?'");
		Оповещение = Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект, РабочееМесто);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;	
КонецПроцедуры  

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да И Не ПустаяСтрока(Параметры) Тогда
		ПоказатьЗначение(, Параметры);
	КонецЕсли;  
	
КонецПроцедуры 

&НаСервере
Функция ПолучитьРабочееМестоПоИдентификаторуКлиента(Идентификатор)
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.Текст="
	|ВЫБРАТЬ
	|    РабочиеМеста.Ссылка
	|ИЗ
	|    Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|    РабочиеМеста.Код = &Идентификатор
	|";
	Выборка=Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Ссылка, Неопределено); 
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ОбщегоНазначенияСервер.ПроверкаРасширения("БиблиотекаПодключаемогоОборудования") Тогда
		ПоказатьПредупреждение(,"Расширение: ""Библиотека подключаемого оборудования"" не подключено!", 30);
		Отказ=Истина; Возврат;
	КонецЕсли;
	
	МодульМенеджерОборудованияКлиентПовтИсп=ОбщегоНазначенияКлиент.ОбщийМодуль("МенеджерОборудованияКлиентПовтИсп");
	ТекущееРабочееМесто=МодульМенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Список.Параметры.УстановитьЗначениеПараметра("ТекущееРабочееМесто", ТекущееРабочееМесто); 
КонецПроцедуры