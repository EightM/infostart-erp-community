////////////////////////////////////////////////////////////////////////////////
// Универсальные

&НаСервере
Функция ЗначениеРеквизитаОбъекта(Ссылка, стрРеквизит)
	Возврат Справочники.РегистрыПравил.ЗначениеРеквизитаОбъекта(Ссылка, стрРеквизит);
КонецФункции

&НаКлиенте
Функция ЭтоИзмерение()
	Возврат ВРег(Объект.Родитель)="ИЗМЕРЕНИЯ";
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Команды

&НаКлиенте
Процедура ВыполнитьКоманду(Команда)
	Если Команда.Имя="АвтоКодАлгоритма" Тогда
		Если ЭтоИзмерение() Тогда
			Объект.ТекстМодуля="ЗначениеИзмерения = СтруктураПараметров.Объект.Дата;";
		Иначе
			Объект.ТекстМодуля="СтруктураПараметров.Отказ = {ЗначениеРесурса};";
		КонецЕсли;
		
	ИначеЕсли Команда.Имя="ВнутреннийИдентификатор" Тогда
		стрИмя=ВнутреннийИдентификатор(Объект.Ссылка);
		Сообщить(стрИмя);
	КонецЕсли;
КонецПроцедуры

Функция ВнутреннийИдентификатор(Ссылка)
	Возврат Справочники.РегистрыПравил.АтрибутВСтроку(Ссылка);
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики оповещения

&НаКлиенте
Процедура ОбработкаОповещения_ВыборНаименования(Параметр1, Параметр2) Экспорт
	Если Параметр1=Неопределено Тогда Возврат; КонецЕсли;
	Если Параметр1.Значение="Пользователь" Тогда
		Объект.ТекстМодуля="ЗначениеИзмерения=ПараметрыСеанса.ТекущийПользователь;";
		ОписаниеТипа=Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		
	ИначеЕсли Параметр1.Значение="ДатаЗапрета" Тогда
		ОписаниеТипа=Новый ОписаниеТипов("Дата");
		Объект.ТекстМодуля="
		|ЗначениеРесурса={ЗначениеРесурса};
		|Если ЗначениеЗаполнено(ЗначениеРесурса) Тогда
		|	СтруктураПараметров.Отказ=КонецДня(СтруктураПараметров.Объект.Дата)<=КонецДня(ЗначениеРесурса);
		|	Если СтруктураПараметров.Событие=""ПриСозданииНаСервере"" Тогда
		|		Если НЕ СтруктураПараметров.Объект.Ссылка.Пустая() Тогда
		|			СтруктураПараметров.Форма.ТолькоПросмотр=СтруктураПараметров.Отказ;
		|		КонецЕсли;
		|		СтруктураПараметров.Отказ=Ложь;
		|	КонецЕсли;
		|	Если СтруктураПараметров.Отказ Тогда
		|		Сообщить(""Доступ закрыт регистром правил!"");
		|	КонецЕсли;
		|КонецЕсли; 
		|";		

	ИначеЕсли Параметр1.Значение="ИсточникДанных" Тогда
		ОписаниеТипа=Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных");
		Объект.ТекстМодуля="ЗначениеИзмерения=Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоРеквизиту(""ПолноеИмя"", СтруктураПараметров.Объект.Ссылка.Метаданные().ПолноеИмя());";

	ИначеЕсли Параметр1.Значение="ВозрастДокумента" Тогда
		ОписаниеТипа=Новый ОписаниеТипов("Число");
		Объект.ТекстМодуля="
		|ЗначениеРесурса={ЗначениеРесурса};
		|Если ЗначениеЗаполнено(ЗначениеРесурса) Тогда
		|	СтруктураПараметров.Отказ=КонецДня(СтруктураПараметров.Объект.Дата)<=ТекущаяДата()-(86400*ЗначениеРесурса);
		|	Если СтруктураПараметров.Событие=""ПриСозданииНаСервере"" Тогда
		|		Если НЕ СтруктураПараметров.Объект.Ссылка.Пустая() Тогда
		|			СтруктураПараметров.Форма.ТолькоПросмотр=СтруктураПараметров.Отказ;
		|		КонецЕсли;
		|		СтруктураПараметров.Отказ=Ложь;
		|	КонецЕсли;
		|	Если СтруктураПараметров.Отказ Тогда
		|		Сообщить(""Доступ закрыт регистром правил!"");
		|	КонецЕсли;
		|КонецЕсли;
		|";
	КонецЕсли;

	Объект.Наименование=Параметр1.Представление;
	Объект.Код=СокрЛП(ЗначениеРеквизитаОбъекта(Объект.Родитель, "Родитель.Код"))+"_"+Параметр1.Значение;
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов шапки

&НаКлиенте
Процедура Атрибут_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элемент.Имя="Наименование" Тогда
		спМеню=Новый СписокЗначений;
		спМеню.Добавить("Пользователь", "Пользователь");
		спМеню.Добавить("ДатаЗапрета", "Дата запрета");
		спМеню.Добавить("ВозрастДокумента", "Возраст документа");
		спМеню.Добавить("ИсточникДанных", "Источник данных");

		ОписаниеОповещения=Новый ОписаниеОповещения("ОбработкаОповещения_ВыборНаименования", ЭтотОбъект);
		ПоказатьВыборИзСписка(ОписаниеОповещения, спМеню, Элементы.Наименование);
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//*** ТолькоПросмотр=Объект.Системный; //Справочники.РегистрыПравил.ЭтоПредопределенныйЭлемент(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ГруппаИнфо.Заголовок="Описание "+?(ЭтоИзмерение(), "измерения", "ресурса");
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОписаниеТипа=ТекущийОбъект.ОписаниеТипа.Получить();
	СписокВыбора=ТекущийОбъект.СписокВыбора.Получить();

	тзБуфер=ТекущийОбъект.ПараметрыВыбора.Получить();
	Если Не тзБуфер=Неопределено Тогда
		Для каждого СтрокаКоллекции Из тзБуфер Цикл
			ЗаполнитьЗначенияСвойств(тзПараметрыВыбора.Добавить(), СтрокаКоллекции);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Не ЗначениеЗаполнено(ОписаниеТипа) Тогда
		Сообщить("Не указан тип!"); Отказ=Истина; Возврат; 
	КонецЕсли; 
	ТекущийОбъект.ОписаниеТипа=Новый ХранилищеЗначения(ОписаниеТипа);
	ТекущийОбъект.СписокВыбора=Новый ХранилищеЗначения(СписокВыбора);
	ТекущийОбъект.ПараметрыВыбора=Новый ХранилищеЗначения(РеквизитФормыВЗначение("тзПараметрыВыбора"));
КонецПроцедуры
