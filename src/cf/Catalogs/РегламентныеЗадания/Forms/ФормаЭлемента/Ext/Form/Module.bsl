&НаКлиенте
Процедура УправлениеФормой()
	СтрокаРасписания=Строка(Расписание);
	Если НЕ ЗначениеЗаполнено(СтрокаРасписания) Тогда
		СтрокаРасписания="Необходимо задать расписание!";
	КонецЕсли;
	Элементы.РасписаниеЗадания.Заголовок=СтрокаРасписания;
КонецПроцедуры

&НаСервере
Процедура ПеречитатьЛог()
	ЭтаФорма.Элементы.СтраницаОтчетПоВыполнению.Доступность=НЕ Объект.Ссылка.Пустая();
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(РезультатВыполнения, "РегламентноеЗадание", Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеЗаданияНажатие(Элемент)
	ПрочитатьРасписание();

	ДиалогРасписания=Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ДиалогРасписания.Показать(Новый ОписаниеОповещения("ОбработчикОповещения_РасписаниеЗадания", ЭтотОбъект));
КонецПроцедуры

&НаСервере
Процедура СохранитьРасписание()
	Объект.Расписание=ЗначениеВСтрокуВнутр(Расписание);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРасписание()
	Если ЗначениеЗаполнено(Объект.Расписание) Тогда
		Расписание=ЗначениеИзСтрокиВнутр(Объект.Расписание);
	Иначе
		Расписание=Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗадание(Команда)
	ВыполнитьТекущееЗадание();
	ПеречитатьЛог();	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьТекущееЗадание()
	Если Модифицированность Тогда
		Сообщить("Предварительно необходимо записать объект");
		Возврат; 
	КонецЕсли;
	МодульРегламентныхЗаданийСервер.ВыполнитьЗаданиеПоНастройке(Объект.Ссылка);	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики оповещения

&НаКлиенте
Процедура ОбработчикОповещения_РасписаниеЗадания(Параметр1, Параметр2) Экспорт
	Если Параметр1=Неопределено Тогда Возврат; КонецЕсли;
	
	Расписание=Параметр1;
	СохранитьРасписание();
	УправлениеФормой();
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПеречитатьЛог();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПрочитатьРасписание();
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.РасписаниеПредставление=Строка(Расписание);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПеречитатьЛог();
КонецПроцедуры