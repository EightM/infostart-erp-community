&НаКлиенте
Процедура ПрименитьНастройку(Команда)
	Закрыть(НастройкиСтрокойXML());
КонецПроцедуры

&НаСервере
Функция НастройкиСтрокойXML()
	Настройки=КомпоновщикНастроек.ПолучитьНастройки();
	Если Настройки.Отбор.Элементы.Количество()=0 Тогда Возврат ""; КонецЕсли; 
	Возврат УниверсальныеМеханизмыСервер.ЗначениеВСтрокуXML(Настройки);
КонецФункции

&НаСервере
Процедура КомпоновщикНастроекИнициализация(стрТипВидОбъекта)
	СхемаКомпоновкиДанных=Новый СхемаКомпоновкиДанных;

	ИсточникДанных=СхемаКомпоновкиДанных.ИсточникиДанных.Добавить(); 
	ИсточникДанных.Имя="ИсточникДанных";
	ИсточникДанных.СтрокаСоединения="";
	ИсточникДанных.ТипИсточникаДанных="Local";
	
	НаборДанных=СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя="НаборДанных";
	НаборДанных.ИсточникДанных="ИсточникДанных";
	НаборДанных.АвтоЗаполнениеДоступныхПолей=Истина;
	НаборДанных.Запрос="Выбрать * Из "+стрТипВидОбъекта+" Как ИсточникДанных";

	АдресСхемыКомпоновкиДанных=ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("ТипВидОбъекта") Тогда
		Отказ=Истина; Возврат;
	КонецЕсли;
	КомпоновщикНастроекИнициализация(Параметры.ТипВидОбъекта);
	Если Параметры.Свойство("Настройки") И ЗначениеЗаполнено(Параметры.Настройки) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(УниверсальныеМеханизмыСервер.ЗначениеИзСтрокиXML(Параметры.Настройки));
	КонецЕсли;
КонецПроцедуры