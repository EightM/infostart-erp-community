&НаКлиенте
Процедура ЗапуститьПлагин(Команда)
	ТекущиеДанные=Элементы.дзДанные.ТекущиеДанные;
	Если ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;
	ПараметыПлагина.Вставить("Плагин", ТекущиеДанные.Ссылка);
	ПлагиныКлиент.Открыть(ТекущиеДанные.Ссылка, ПараметыПлагина, ВладелецФормы);
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Функция дзДанные_СоздатьВеткуДерева(РодительДерева, РодительПлагина, ВладелецПлагина)
	Выборка=Справочники.Плагины.Выбрать(РодительПлагина, ВладелецПлагина);
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПометкаУдаления Тогда Продолжить; КонецЕсли;

		Если Выборка.Доступ.Количество()>0 Тогда
			ДоступРазрешен=Ложь;
			Для каждого СтрокаКоллекции Из Выборка.Доступ Цикл
				Если СтрокаКоллекции.Пользователь.ЭтоГруппа Тогда
					Если ПараметрыСеанса.ТекущийПользователь.ПринадлежитЭлементу(СтрокаКоллекции.Пользователь) Тогда
						ДоступРазрешен=Истина; Прервать;
					КонецЕсли; 
				Иначе
					Если СтрокаКоллекции.Пользователь=ПараметрыСеанса.ТекущийПользователь Тогда
						ДоступРазрешен=Истина; Прервать;
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;			
			Если НЕ ДоступРазрешен Тогда Продолжить; КонецЕсли; 
		КонецЕсли;

		НоваяСтрока=РодительДерева.Строки.Добавить();	
		НоваяСтрока.Ссылка=Выборка.Ссылка;
		НоваяСтрока.Картинка=БиблиотекаКартинок.Элемент;
		Если Выборка.ЭтоГруппа Тогда
			НоваяСтрока.Картинка=БиблиотекаКартинок.Группа;
			дзДанные_СоздатьВеткуДерева(НоваяСтрока, Выборка.Ссылка, ВладелецПлагина);
		КонецЕсли;
	КонецЦикла;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("Владелец") Тогда
		Отказ=Истина; Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.Ссылка) Тогда
		Заголовок=СокрЛП(Параметры.Ссылка);
	Иначе
		Заголовок=СокрЛП(Параметры.Владелец);
	КонецЕсли; 
	
	//стрПолноеИмя=Параметры.Ссылка.Метаданные().ПолноеИмя();
	//Если Найти(стрПолноеИмя, "Документ.")>0 Тогда
	//	Заголовок="Плагины документа ("+СокрЛП(Параметры.Ссылка)+")";
	//Иначе
	//	Заголовок="Плагины элемента ("+СокрЛП(Параметры.Ссылка)+")";
	//КонецЕсли;		

	ПараметыПлагина=Новый Структура;
	ПараметыПлагина.Вставить("Ссылка", Параметры.Ссылка);
	
	дзСсылка=РеквизитФормыВЗначение("дзДанные");
	дзДанные_СоздатьВеткуДерева(дзСсылка, Справочники.Плагины.ПустаяСсылка(), Параметры.Владелец);
	ЗначениеВРеквизитФормы(дзСсылка, "дзДанные");
	
	ЕстьПлагины=дзСсылка.Строки.Количество();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЕстьПлагины Тогда
		Отказ=Истина; ПоказатьПредупреждение(, "Плагины не подключены!", 20);
	КонецЕсли; 	
КонецПроцедуры
