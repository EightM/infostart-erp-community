&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="РедакторВстроенногоЯзыка" Тогда
		СтруктураПараметров=Новый Структура;
		СтруктураПараметров.Вставить("Алгоритм", Объект.Алгоритм);
		ОписаниеОповещения=Новый ОписаниеОповещения("ОбработчикОповещения_РедакторВстроенногоЯзыка", ЭтотОбъект);
		ОткрытьФорму("Справочник.Алгоритмы.Форма.ФормаРедактораВстроенногоЯзыка", СтруктураПараметров, ЭтаФорма,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	ИначеЕсли Команда.Имя="СохранитьФайл" Тогда
		ДиалогФайла=Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогФайла.ПолноеИмяФайла=СтрПолучитьСтроку(Объект.Алгоритм, 1);
		ДиалогФайла.Расширение="epf";
		ДиалогФайла.Фильтр="Внешняя обработка(*.epf)|*.epf";
		ДиалогФайла.Показать(Новый ОписаниеОповещения("ОбработчикОповещения_СохранениеФайлаКлиент", ЭтотОбъект));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьЭлементовФормы()
	Элементы.СтраницаАлгоритм.Видимость=Объект.ТипПлагина=2;
	Элементы.СохранитьФайл.Видимость=Объект.ТипПлагина=1;
	Элементы.Алгоритм.Доступность=Объект.ТипПлагина=2;
	Элементы.РедакторВстроенногоЯзыка.Видимость=Объект.ТипПлагина=2;
КонецПроцедуры

&НаСервере
Процедура Очистить()
	РеквизитФормыВЗначение("Объект").Хранилище=Неопределено;
	Объект.Алгоритм="";		
	Объект.Наименование="";
КонецПроцедуры

&НаСервере
Функция СписокВстроенныхОбработок()
	СписокВыбора=Новый СписокЗначений;
	Для каждого мдОбъект Из Метаданные.Обработки Цикл
		СписокВыбора.Добавить(мдОбъект.Имя, мдОбъект.Представление());
	КонецЦикла;
	Возврат СписокВыбора;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики оповещений

&НаКлиенте
Процедура ОбработчикОповещения_СохранениеФайлаКлиент(Параметр1, Параметр2) Экспорт
	Если НЕ ТипЗнч(Параметр1)=Тип("Массив") Тогда Возврат; КонецЕсли; 
	ОбработчикОповещения_СохранениеФайлаСервер(Параметр1[0]);
КонецПроцедуры

&НаСервере
Процедура ОбработчикОповещения_СохранениеФайлаСервер(стрПутьКФайлу)
	ДвоичныеДанные=РеквизитФормыВЗначение("Объект").Хранилище.Получить();
	Если ДвоичныеДанные=Неопределено Тогда Возврат; КонецЕсли;

	Попытка ДвоичныеДанные.Записать(стрПутьКФайлу);
	Исключение ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_РедакторВстроенногоЯзыка(Параметр1, Параметр2) Экспорт
	Если Параметр1=Неопределено Тогда Возврат; КонецЕсли;
	Объект.Алгоритм=Параметр1;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_ВыборФайлаКлиент(Параметр1, Параметр2) Экспорт
	Если НЕ ТипЗнч(Параметр1)=Тип("Массив") Тогда Возврат; КонецЕсли; 
	ОбработчикОповещения_ВыборФайлаСервер(Параметр1[0]);
КонецПроцедуры

&НаСервере
Процедура ОбработчикОповещения_ВыборФайлаСервер(стрПутьКФайлу)
	Попытка
		СообщениеОшибки="Выбранный файл не является внешней обработкой.
		|Либо, данная обработка не предназначена для запуска в этой конфигурации.";
		ВнешняяОбработка=ВнешниеОбработки.Создать(стрПутьКФайлу);
		Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
			Объект.Наименование=ВнешняяОбработка.Метаданные().Представление();
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;

	Файл=Новый Файл(стрПутьКФайлу); ПутьКФайлу=стрПутьКФайлу;
	Объект.Алгоритм=Файл.Имя+Символы.ПС+"изменен:"+Файл.ПолучитьВремяИзменения()+Символы.ПС+"сохранен в ИБ:"+ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_ВыборВстроеннойОбработки(Параметр1, Параметр2) Экспорт
	Если Параметр1=Неопределено Тогда Возврат; КонецЕсли;
	Объект.Наименование=Параметр1.Представление;
	Объект.Алгоритм=Параметр1.Значение;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов шапки

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="ТипПлагина" Тогда
		Очистить();
		ВидимостьЭлементовФормы();
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элемент.Имя="Наименование" Тогда
		Если Объект.ТипПлагина=0 Тогда
			спМеню=СписокВстроенныхОбработок();
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ОбработчикОповещения_ВыборВстроеннойОбработки", ЭтотОбъект), спМеню);
			
		ИначеЕсли Объект.ТипПлагина=1 Тогда
			ДиалогФайла=Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
			ДиалогФайла.МножественныйВыбор=Ложь;
			ДиалогФайла.Фильтр="Внешняя обработка(*.epf)|*.epf";
			ДиалогФайла.Показать(Новый ОписаниеОповещения("ОбработчикОповещения_ВыборФайлаКлиент", ЭтотОбъект));
		КонецЕсли;
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_Очистка(Элемент, СтандартнаяОбработка)
	Если Элемент.Имя="Наименование" Тогда
		Очистить();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.Владелец.Доступность=Объект.Владелец.Пустая();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидимостьЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ ПустаяСтрока(ПутьКФайлу) Тогда
		ТекущийОбъект.Хранилище=Новый ХранилищеЗначения(Новый ДвоичныеДанные(ПутьКФайлу));
	КонецЕсли;
КонецПроцедуры
