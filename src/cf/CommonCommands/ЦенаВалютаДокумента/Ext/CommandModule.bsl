&НаСервере
Функция ВалютаВзаиморасчетов(Объект)
	Если Объект.Ссылка.Метаданные().Реквизиты.Найти("ДоговорКонтрагента")=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	Возврат Объект.ДоговорКонтрагента.ВалютаВзаиморасчетов;
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если Не ТипЗнч(ПараметрыВыполненияКоманды.Источник)=Тип("УправляемаяФорма") Тогда Возврат; КонецЕсли; 

	Объект=ПараметрыВыполненияКоманды.Источник.Объект;

	МассивРеквизитов=Новый Массив;
	МассивРеквизитов.Добавить("ТипЦен");
	МассивРеквизитов.Добавить("ВалютаДокумента");
	МассивРеквизитов.Добавить("КурсДокумента");
	МассивРеквизитов.Добавить("КратностьДокумента");
	МассивРеквизитов.Добавить("КурсВзаиморасчетов");
	МассивРеквизитов.Добавить("КратностьВзаиморасчетов");
	МассивРеквизитов.Добавить("УчитыватьНДС");
	МассивРеквизитов.Добавить("СуммаВключаетНДС");
	МассивРеквизитов.Добавить("РегистрироватьЦеныПоставщика");
	МассивРеквизитов.Добавить("УсловиеПродаж");

	СтруктураРеквизитов=Новый Структура;
	Для каждого ИмяРеквизита Из МассивРеквизитов Цикл
		Если НЕ Объект.Свойство(ИмяРеквизита) Тогда Продолжить; КонецЕсли;
		СтруктураРеквизитов.Вставить(ИмяРеквизита, Объект[ИмяРеквизита]);		
	КонецЦикла;

	СтруктураВалюты=ВалютныйУчетСервер.КурсВалюты(Объект.Дата);
	Если НЕ СтруктураРеквизитов.Свойство("КурсДокумента") Тогда
		СтруктураРеквизитов.Вставить("КурсДокумента", СтруктураВалюты.Курс);
	КонецЕсли; 
	Если НЕ СтруктураРеквизитов.Свойство("КратностьДокумента") Тогда
		СтруктураРеквизитов.Вставить("КратностьДокумента", СтруктураВалюты.Кратность);
	КонецЕсли;

	СтруктураРеквизитов.Вставить("ДатаКурса", Объект.Дата);
	
	Валюта=ВалютаВзаиморасчетов(Объект);
	Если ЗначениеЗаполнено(Валюта) Тогда
		СтруктураРеквизитов.Вставить("ВалютаВзаиморасчетов", Валюта);
	КонецЕсли; 

	ОповещениеПриЗакрытии=Новый ОписаниеОповещения("ОбработчикОповещения_ЦенаВалюта", ПараметрыВыполненияКоманды.Источник);

	ОткрытьФорму("ОбщаяФорма.ЦенаВалютаДокумента", СтруктураРеквизитов, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка, ОповещениеПриЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
