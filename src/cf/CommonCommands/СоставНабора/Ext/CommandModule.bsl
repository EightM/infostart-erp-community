&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Форма=ПараметрыВыполненияКоманды.Источник;
	Объект=Форма.Объект;
	Элементы=Форма.Элементы;

	ТекущиеДанные=Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;

	Если НЕ ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Номенклатура, "Комплект") Тогда
		ПоказатьПредупреждение(, "Номенклатура не является набором-комплектом!", 15, "Внимание"); Возврат;
	КонецЕсли;
	
	ПараметрыФормы=Новый Структура("Номенклатура,Количество,ЕдиницаИзмерения,ID");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
	ПараметрыФормы.Вставить("ТипЦен", Объект.ТипЦен);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	ПараметрыФормы.Вставить("Склад", Объект.Склад);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);	
	ПараметрыФормы.Вставить("СоставНабора", Объект.СоставНабора);
	ПараметрыФормы.Вставить("Ссылка", Форма.Объект.Ссылка);
	ПараметрыФормы.Вставить("Объект", Форма.Объект);
	//**ПараметрыФормы.Вставить("СтруктураОбъекта", УправлениеКонфигурациейСервер.СтруктураОбъекта(Форма.ИмяФормы));

	ОписаниеОповещения=Новый ОписаниеОповещения("ОбработчикОповещения_СоставНабора", ЭтотОбъект, Новый Структура("Объект,ТекущиеДанные", Объект, ТекущиеДанные));
	ОткрытьФорму("Обработка.СоставНабора.Форма.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_СоставНабора(Параметр1, Параметр2) Экспорт
	Если Параметр1=Неопределено Тогда Возврат; КонецЕсли;
	Если Параметр1.Количество()=0 Тогда Возврат; КонецЕсли;

	МассивСтрок=Параметр2.Объект.СоставНабора.НайтиСтроки(Новый Структура("ID_Товары", Параметр2.ТекущиеДанные.ID));
	Для каждого СтрокаКоллекции Из МассивСтрок Цикл
		Параметр2.Объект.СоставНабора.Удалить(СтрокаКоллекции);
	КонецЦикла;
	
	Для каждого СтрокаКоллекции Из Параметр1 Цикл
		ЗаполнитьЗначенияСвойств(Параметр2.Объект.СоставНабора.Добавить(), СтрокаКоллекции);
	КонецЦикла; 
КонецПроцедуры