
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Код1994 = Параметры.Код1994;
	Наименование1994 = Параметры.Наименование1994;
	ОКОФ2014 = Параметры.ОКОФ2014;
	Объект.Организация = Параметры.Организация;
	Объект.ГруппаОбъектов = Параметры.ГруппаОбъектов;
	Объект.КодыОКОФПообъектно.Загрузить(Параметры.КодыОКОФПообъектно.Выгрузить());
	
	ЗаполнитьТаблицуЗамены();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
			
	ОповеститьОВыборе(ПолучитьРезультат());
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьВсе(Команда)
	
	Для Каждого Стр Из ТаблицаЗамены Цикл
		
		Стр.Отметка = Истина;
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура СнятьПометки(Команда)
	
	Для Каждого Стр Из ТаблицаЗамены Цикл
		
		Стр.Отметка = Ложь;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьРезультат()
	
	Результат = Новый Массив;
	
			
	Для Каждого Стр Из ТаблицаЗамены Цикл
		
		Если Стр.Отметка Тогда
			
			ТабРезультат = Новый Структура("Код1994, ОКОФ2014, ОбъектОС, КодОС, ИнвНомер");
			ТабРезультат.Код1994 = Код1994;
			ТабРезультат.ОКОФ2014 = Стр.ОКОФ2014;
			ТабРезультат.ОбъектОС = Стр.ОбъектОС;
			ТабРезультат.КодОС = Стр.КодОС;
			ТабРезультат.ИнвНомер = Стр.ИнвНомер;
			
			Результат.Добавить(ТабРезультат);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

&НаСервере
Процедура ЗаполнитьТаблицуЗамены()
	
	
	//Уже выбранные из базы и помещенные в таблицу
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КодыОКОФПообъектно.ОКОФ2014 КАК ОКОФ2014,
	|	КодыОКОФПообъектно.ОбъектОС КАК ОбъектОС,
	|	КодыОКОФПообъектно.КодОС КАК КодОС,
	|	КодыОКОФПообъектно.ИнвНомер КАК ИнвНомер
	|ПОМЕСТИТЬ ВыбранныеООбъекты
	|ИЗ
	|	&КодыОКОФПообъектно КАК КодыОКОФПообъектно
	|ГДЕ
	|	КодыОКОФПообъектно.Код1994 = &Код1994
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыбранныеООбъекты.ОКОФ2014 КАК ОКОФ2014,
	|	ВыбранныеООбъекты.ОбъектОС КАК ОбъектОС,
	|	ВыбранныеООбъекты.КодОС КАК КодОС,
	|	ВыбранныеООбъекты.ИнвНомер КАК ИнвНомер,
	|	ИСТИНА КАК Отметка
	|ИЗ
	|	ВыбранныеООбъекты КАК ВыбранныеООбъекты");
	
	Запрос.УстановитьПараметр("КодыОКОФПообъектно", Объект.КодыОКОФПообъектно.Выгрузить());
	Запрос.УстановитьПараметр("Код1994", Код1994);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовСтр = ТаблицаЗамены.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		
	КонецЦикла;
	
	Если ТаблицаЗамены.Количество() = 0 Тогда
		Отметка = Истина;
	Иначе
		Отметка = Ложь;
	КонецЕсли;	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВыбранныеОС.ОбъектОС КАК ОбъектОС
	|ПОМЕСТИТЬ ВыбранныеОС
	|ИЗ
	|	&ВыбранныеОС КАК ВыбранныеОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОбъектОС,
	|	ОсновныеСредства.КодПоОКОФ КАК ОКОФ1994,
	|	&ОКОФ2014 КАК ОКОФ2014,
	|	ОсновныеСредства.Код КАК КодОС,
	|	ОсновныеСредства.Код КАК ИнвНомер
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.КодПоОКОФ.Код = &Код1994
	|	И НЕ ОсновныеСредства.Ссылка В
	|				(ВЫБРАТЬ
	|					ВыбранныеОС.ОбъектОС
	|				ИЗ
	|					ВыбранныеОС КАК ВыбранныеОС)");
	
	Запрос.УстановитьПараметр("ВыбранныеОС", ТаблицаЗамены.Выгрузить());
	Запрос.УстановитьПараметр("ОКОФ2014", ОКОФ2014);
	Запрос.УстановитьПараметр("Код1994", Код1994);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	ОтборОС = СхемаЗапроса.ПакетЗапросов[1].Операторы[0].Отбор;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ТекстУсловия = "ОсновныеСредства.Организация = &Организация"; 
		ОтборОС.Добавить(ТекстУсловия);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ГруппаОбъектов) Тогда
		
		ТекстУсловия = "ОсновныеСредства.Ссылка В ИЕРАРХИИ(&ГруппаОбъектов)"; 
		ОтборОС.Добавить(ТекстУсловия);
		Запрос.УстановитьПараметр("ГруппаОбъектов", Объект.ГруппаОбъектов);
		
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовСтр = ТаблицаЗамены.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		НовСтр.Отметка = Отметка; 
		
	КонецЦикла;
	
КонецПроцедуры

	
#КонецОбласти



