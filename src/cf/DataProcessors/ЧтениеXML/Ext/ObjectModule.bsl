&НаСервере
Функция ДеревоЗначений(ДанныеXML, ЭтоФайл=Ложь) Экспорт
	Чтение=Новый ЧтениеXML();
	Если ЭтоФайл Тогда
		Чтение.ОткрытьФайл(ДанныеXML);
	Иначе
		Чтение.УстановитьСтроку(ДанныеXML);
	КонецЕсли; 

	Попытка Чтение.Прочитать();
	Исключение Возврат Новый ДеревоЗначений;
	КонецПопытки;

	ДеревоЗначений=Новый ДеревоЗначений;
	ДеревоЗначений.Колонки.Добавить("Элемент");
	ДеревоЗначений.Колонки.Добавить("Текст");
	ДеревоЗначений.Колонки.Добавить("Атрибуты", Новый ОписаниеТипов("Соответствие"));
	
	Корень=ДеревоЗначений.Строки.Добавить();
	Корень.Элемент=Чтение.Имя;
	Рекурсия(Корень, Чтение);
	
	Массив=Новый Массив;
	Массив.Добавить(ДеревоЗначений);

	Возврат ДеревоЗначений;
КонецФункции

&НаСервере
Процедура Рекурсия(СтрокаДерева, Чтение)
	Если Чтение.КоличествоАтрибутов() > 0 Тогда
		АтрибутыСоответствие = Новый Соответствие();
		Пока Чтение.ПрочитатьАтрибут() Цикл
			АтрибутыСоответствие.Вставить(Чтение.Имя, Чтение.Значение);
		КонецЦикла;
		СтрокаДерева.Атрибуты = АтрибутыСоответствие;
	КонецЕсли;
	Пока Чтение.Прочитать() Цикл
		Если Чтение.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Прервать;
		ИначеЕсли Чтение.ТипУзла = ТипУзлаXML.Текст Тогда
			СтрокаДерева.Текст = Чтение.Значение;
		ИначеЕсли Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Дочерний=СтрокаДерева.Строки.Добавить();
			Дочерний.Элемент=Чтение.Имя;
			Рекурсия(Дочерний, Чтение);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры