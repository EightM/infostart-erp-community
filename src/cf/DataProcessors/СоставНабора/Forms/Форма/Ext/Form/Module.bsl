&НаКлиенте
Процедура ОК(Команда)
	ЭтаФорма.Закрыть(Объект.Товары);
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеОтображения()
	СуммаКомплекта=0;
	Для каждого СтрокаКоллеции Из Объект.Товары Цикл
		СуммаКомплекта=СуммаКомплекта+СтрокаКоллеции.Количество*СтрокаКоллеции.Цена;
	КонецЦикла;
	Элементы.ГруппаНабор1.Заголовок="Набор ( стоимость: "+ОбщегоНазначения.ФорматСумм(СуммаКомплекта)+")";
КонецПроцедуры

&НаСервере
Функция СформироватьСписокСпецификаций()
	спМеню=Новый СписокЗначений;
	Выборка=Справочники.СпецификацияНоменклатуры.Выбрать(, Объект.Номенклатура,,"Код Возр");
	Пока Выборка.Следующий() Цикл
		спМеню.Добавить(Выборка.Ссылка);
	КонецЦикла; 
	Если спМеню.Количество()=0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Номеклатура не имеет спецификаций");
		Возврат спМеню;
	КонецЕсли;
	Возврат спМеню;
КонецФункции	

&НаСервереБезКонтекста
Функция СпецификацияНаДату(ВариантСпецификации)
	Возврат Документы.Спецификация.ПолучитьСпецификациюНаДату(ВариантСпецификации);
КонецФункции

&НаСервере
Процедура ОбработчикОповещения_ЗаполнитьПоСпецификации_Сервер(ДокСпецификация)
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("Дата", Объект.Дата);
	Для каждого СтрокаКоллекции Из ДокСпецификация.Комплектующие Цикл
		НормаРасхода=СтрокаКоллекции.НормаРасхода;
		Если НормаРасхода=0 Тогда НормаРасхода=1; КонецЕсли; 
		
		НоваяСтрока=Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКоллекции);
		НоваяСтрока.Количество=Объект.Количество*Окр(СтрокаКоллекции.Количество/НормаРасхода, СтрокаКоллекции.ОкруглятьДоКоличествоЗнаков);
		НоваяСтрока.Качество=Справочники.Качество.Новый;
		Если Не Объект.ТипЦен.Пустая() Тогда
			СтруктураПараметров.Вставить("ЕдиницаИзмерения", СтрокаКоллекции.ЕдиницаИзмерения);
			НоваяСтрока.Цена=ЦенообразованиеСервер.ЦенаНоменклатуры(СтрокаКоллекции.Номенклатура, СтрокаКоллекции.ХарактеристикаНоменклатуры, Объект.ТипЦен, СтруктураПараметров);
		КонецЕсли;
		НоваяСтрока.ID_Товары=Объект.ID;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_ЗаполнитьПоСпецификации_Клиент(ВариантСпецификации, Параметр2=Неопределено) Экспорт
	ДокСпецификация=СпецификацияНаДату(ВариантСпецификации.Значение);
	Если НЕ ЗначениеЗаполнено(ДокСпецификация) Тогда Возврат; КонецЕсли;
	Объект.Товары.Очистить();
	ОбработчикОповещения_ЗаполнитьПоСпецификации_Сервер(ДокСпецификация)
КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="Организация" Или Элемент.Имя="Склад" Тогда		
		тпТовары_ОбновитьОстатки();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличной части "Состав набора"

&НаКлиенте
Процедура тпТовары_ВыполнитьДействие(Команда)
	Если Команда.Имя="ЗаполнитьПоСпецификации" Тогда
		спМеню=СформироватьСписокСпецификаций();
		Если спМеню.Количество()=0 Тогда Возврат; КонецЕсли;
		
		Если спМеню.Количество()=1 Тогда
			ОбработчикОповещения_ЗаполнитьПоСпецификации_Клиент(спМеню[0]);
		Иначе
			спМеню.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ОбработчикОповещения_ЗаполнитьПоСпецификации_Клиент", ЭтотОбъект), "Выберите вариант спецификации", Элементы.Товары);
		КонецЕсли;

	ИначеЕсли Команда.Имя="ОбновитьОстатки" Тогда
		тпТовары_ОбновитьОстатки();

	ИначеЕсли Команда.Имя="ЗаполнитьТОП" Тогда
		тпТовары_ЗаполнитьТОП();
		
	ИначеЕсли Команда.Имя="РазвернутыеОстатки" Тогда
		Если Объект.Товары.Количество()=0 Тогда
			ПоказатьПредупреждение(, "Таблица состав набора не заполнена!", 20, "Внимание");
			Возврат;
		КонецЕсли;

		спНоменклатура=Новый СписокЗначений;
		Для каждого СтрокаКоллекции Из Элементы.Товары.ВыделенныеСтроки Цикл
			спНоменклатура.Добавить(Объект.Товары[СтрокаКоллекции].Номенклатура);
		КонецЦикла; 

		СтруктураОтбора=Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура", спНоменклатура);
		Если НЕ Объект.Организация.Пустая() Тогда
		    СтруктураОтбора.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		Если НЕ Объект.Организация.Пустая() Тогда
			СтруктураОтбора.Вставить("Склад", Объект.Склад);
		КонецЕсли;

		ПараметрыФормы=Новый Структура;
		ПараметрыФормы.Вставить("Дата", Объект.Дата);
		ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);

		ОткрытьФорму("Обработка.ОстаткиТМЦ.Форма.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура тпТовары_ПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ID=Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура тпТовары_ПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ID_Товары=Объект.ID;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура тпТовары_Колонка_ПриИзменении(Элемент)
	стрКолонка=стрЗаменить(Элемент.Имя, "Товары", "");
	ТекущиеДанные=Элементы.Товары.ТекущиеДанные;
	
	Если стрКолонка="Номенклатура" Тогда
		ТекущиеДанные.ЕдиницаИзмерения=ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Номенклатура, "ЕдиницаХраненияОстатков");
		Если ТекущиеДанные.Количество=0 Тогда ТекущиеДанные.Количество=1; КонецЕсли;
		тпТовары_УстановитьЦену(ТекущиеДанные);

	ИначеЕсли стрКолонка="ХарактеристикаНоменклатуры" Тогда
		Если НЕ ТекущиеДанные.ХарактеристикаНоменклатуры.Владелец=ТекущиеДанные.Номенклатура Тогда
			ТекущиеДанные.Номенклатура=ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ТекущиеДанные.ХарактеристикаНоменклатуры, "Владелец");
			ТекущиеДанные.ЕдиницаИзмерения=ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Номенклатура, "ЕдиницаХраненияОстатков");
		КонецЕсли;
		тпТовары_УстановитьЦену(ТекущиеДанные);

	ИначеЕсли стрКолонка="ЕдиницаИзмерения" Тогда
		тпТовары_УстановитьЦену(ТекущиеДанные);

	ИначеЕсли стрКолонка="Количество" Тогда
		ОбновлениеОтображения();

	ИначеЕсли стрКолонка="Цена" Тогда
		ОбновлениеОтображения();
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура тпТовары_УстановитьЦену(ТекущиеДанные)
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("Дата", Объект.Дата);
	СтруктураПараметров.Вставить("ЕдиницаИзмерения", ТекущиеДанные.ЕдиницаИзмерения);	
	ТекущиеДанные.Цена=ЦенообразованиеСервер.ЦенаПродажи(ТекущиеДанные.Номенклатура, ТекущиеДанные.ХарактеристикаНоменклатуры, Объект.ТипЦен, СтруктураПараметров);
	ОбновлениеОтображения();
КонецПроцедуры	

&НаСервере
Процедура тпТовары_ОбновитьОстатки()
	стрУсловие1="Номенклатура В (&Номенклатура) И ХарактеристикаНоменклатуры В (&ХарактеристикаНоменклатуры)";
	Если НЕ Объект.Организация.Пустая() Тогда
		стрУсловие1=стрУсловие1+" И Организация=&Организация";
	КонецЕсли;
	Если НЕ Объект.Склад.Пустая() Тогда
		стрУсловие1=стрУсловие1+" И Склад=&Склад";
	КонецЕсли;
	стрУсловие2=стрУсловие1+" И СерияНоменклатуры  В (&СерияНоменклатуры)";

	МассивНоменклатура=Новый Массив;
	МассивСерия=Новый Массив;
	МассивХарактеристика=Новый Массив;	
	Для каждого СтрокаКоллекции Из Объект.Товары Цикл
		МассивНоменклатура.Добавить(СтрокаКоллекции.Номенклатура);
		МассивСерия.Добавить(СтрокаКоллекции.СерияНоменклатуры);
		МассивХарактеристика.Добавить(СтрокаКоллекции.ХарактеристикаНоменклатуры);
	КонецЦикла; 
	
	Запрос=Новый Запрос;
	Запрос.Параметры.Вставить("МоментВремени", Объект.Дата);
	Запрос.Параметры.Вставить("Организация", Объект.Организация);
	Запрос.Параметры.Вставить("Склад", Объект.Склад);
	Запрос.Параметры.Вставить("Номенклатура", МассивНоменклатура);
	Запрос.Параметры.Вставить("ХарактеристикаНоменклатуры", МассивХарактеристика);
	Запрос.Параметры.Вставить("СерияНоменклатуры", МассивСерия);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	ИсточникДанных1.Номенклатура Как Номенклатура,
	|	ИсточникДанных1.ХарактеристикаНоменклатуры Как ХарактеристикаНоменклатуры,
	|	ИсточникДанных1.СерияНоменклатуры Как СерияНоменклатуры,
	|	ИсточникДанных1.КоличествоОстаток Как Остаток,
	|	0 Как Потребность,
	|	0 Как Резерв
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(&МоментВремени, "+стрУсловие2+") КАК ИсточникДанных1
	|";
	
	Если УчетПотребностей Тогда
	  Запрос.Текст=Запрос.Текст+"
	  |ОБЪЕДИНИТЬ ВСЕ
	  |	
	  |ВЫБРАТЬ
	  |	ИсточникДанных1.Номенклатура Как Номенклатура,
	  |	ИсточникДанных1.ХарактеристикаНоменклатуры Как ХарактеристикаНоменклатуры,
	  |	Неопределено,
	  |	0 Как Остаток,
	  |	ИсточникДанных1.КоличествоОстаток Как Потребность,
	  |	0 Как Резерв
	  |ИЗ
	  |	РегистрНакопления.УчетПотребностей.Остатки(&МоментВремени, "+стрУсловие1+") КАК ИсточникДанных1
	  |";
	КонецЕсли; 

	Если УчетРезервов Тогда
	  Запрос.Текст=Запрос.Текст+"
	  |ОБЪЕДИНИТЬ ВСЕ
	  |
	  |ВЫБРАТЬ
	  |	ИсточникДанных1.Номенклатура Как Номенклатура,
	  |	ИсточникДанных1.ХарактеристикаНоменклатуры Как ХарактеристикаНоменклатуры,
	  |	ИсточникДанных1.СерияНоменклатуры Как СерияНоменклатуры,
	  |	0 Как Остаток,
	  |	0 Как Потребность,
	  |	ИсточникДанных1.КоличествоОстаток Как Резерв
	  |ИЗ
	  |	РегистрНакопления.УчетРезервовТМЦ.Остатки(&МоментВремени, "+стрУсловие2+") КАК ИсточникДанных1
	  |";
	КонецЕсли; 

	тзДанные=Запрос.Выполнить().Выгрузить();
	тзДанные.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,СерияНоменклатуры", "Остаток,Потребность,Резерв");

	СтруктураПоиска=Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,СерияНоменклатуры");
	Для каждого СтрокаКоллекции Из Объект.Товары Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаКоллекции);
		МассивСтрок=тзДанные.НайтиСтроки(СтруктураПоиска);
		Если МассивСтрок.Количество()=0 Тогда Продолжить; КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаКоллекции, МассивСтрок[0], "Остаток,Потребность,Резерв");
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура тпТовары_ЗаполнитьТОП()
	ОбъектДанные=РеквизитФормыВЗначение("Объект");
	Для каждого СтрокаТабличнойЧасти Из ОбъектДанные["Товары"] Цикл
		УправлениеТиповымиОперациямиСервер.УстановитьТиповуюОперацию(СтрокаТабличнойЧасти, ДокументСсылка, ЭтаФорма, "Товары");
	КонецЦикла;
	ЗначениеВРеквизитФормы(ОбъектДанные, "Объект");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(Объект, Параметры);	
	Параметры.Свойство("Ссылка", ДокументСсылка);
	//ДанныеФормыВЗначение(Параметры.Объект, ТипЗнч(Параметры.Объект));
	//ДанныеФормыВЗначение(Объект, ТипЗнч(Объект));
	
	Если НЕ ЗначениеЗаполнено(Объект.Номенклатура) Тогда Отказ=Истина; Возврат;	КонецЕсли;

	тзДанные=Параметры.СоставНабора.Выгрузить(Параметры.СоставНабора.НайтиСтроки(Новый Структура("ID_Товары", Объект.ID)));
	Объект.Товары.Загрузить(тзДанные);

	УчетРезервов=Константы.УчетРезервов.Получить();
	Элементы.ТоварыРезерв.Видимость=УчетРезервов;

	УчетПотребностей=Константы.УчетПотребностей.Получить();
	Элементы.ТоварыПотребность.Видимость=УчетПотребностей;

	тпТовары_ОбновитьОстатки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		ПоказатьПредупреждение(, "Не выбрана номенклатура!");
		Отказ=Истина; Возврат;
	КонецЕсли;
	ОбновлениеОтображения();
КонецПроцедуры
