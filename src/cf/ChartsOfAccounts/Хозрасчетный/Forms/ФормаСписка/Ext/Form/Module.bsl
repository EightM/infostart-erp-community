&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="Печать1" Или Команда.Имя="Печать2" Тогда
		ТабличныйДокумент=ПечатьПланаСчетов(Команда.Имя="Печать2");

		ПараметрыФормы=Новый Структура;		
		ПараметрыФормы.Вставить("ПечатныйДокумент", ТабличныйДокумент);
		ПараметрыФормы.Вставить("КоличествоЭкземпляров", 1);
		ПараметрыФормы.Вставить("НаПринтер", Ложь);
		ПараметрыФормы.Вставить("ПараметрыПечатнойФормы", Новый Структура);
		ПараметрыФормы.Вставить("ОбъектПечати", "План счетов хозрасчетный");

		ОткрытьФорму("Справочник.ПечатныеФормы.Форма.ФормаПечати", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПечатьПланаСчетов(ВыводитьОписания=Ложь)
	Макет=ПланыСчетов.Хозрасчетный.ПолучитьМакет("Описание");

	Шапка=Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Заголовок=ЭтаФорма.Заголовок;
	
	ТабДокумент=Новый ТабличныйДокумент;
	ТабДокумент.Вывести(Шапка);
	
	Запрос=Новый Запрос;
	Запрос.Текст="
	|ВЫБРАТЬ
	|	ПланСчетов.Ссылка КАК Ссылка,
	|	ПланСчетов.ЭтоГруппа КАК ЭтоГруппа,
	|	ПланСчетов.Ссылка.Код КАК Код,
	|	ПланСчетов.Ссылка.Наименование КАК Наименование,
	|	ПланСчетов.Ссылка.Валютный КАК Валютный,
	|	ПланСчетов.Ссылка.Количественный КАК Количественный,
	|	ПланСчетов.Ссылка.Забалансовый КАК Забалансовый,
	|	ПланСчетов.Ссылка.Вид КАК Вид,
	|	ПланСчетов.Ссылка.ВидыСубконто.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВидСубконто.Наименование КАК Наименование,
	|		ТолькоОбороты КАК ТолькоОбороты
	|	) КАК ВидыСубконто
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПланСчетов1.Ссылка КАК Ссылка,
	|		ВЫБОР
	|			КОГДА КОЛИЧЕСТВО(ПланСчетов2.Ссылка) > 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК ЭтоГруппа
	|	ИЗ
	|		ПланСчетов.Хозрасчетный КАК ПланСчетов1
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК ПланСчетов2
	|			ПО ПланСчетов1.Ссылка = ПланСчетов2.Родитель
	|	ГДЕ
	|		ПланСчетов1.ПометкаУдаления = ЛОЖЬ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПланСчетов1.Ссылка) КАК ПланСчетов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПланСчетов.Ссылка.Порядок
	|";
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда
			Строка=Макет.ПолучитьОбласть("Группа");
		Иначе
			Строка=Макет.ПолучитьОбласть("Строка");
		КонецЕсли;

		Строка.Параметры.Заполнить(Выборка);

		Если Выборка.Вид=ВидСчета.Активный Тогда
			Строка.Параметры.Активность="А";
		ИначеЕсли Выборка.Вид = ВидСчета.Пассивный Тогда
			Строка.Параметры.Активность="П";
		Иначе
			Строка.Параметры.Активность="АП";
		КонецЕсли;
		
		ВидыСубконто=Выборка.ВидыСубконто.Выбрать();
		Пока ВидыСубконто.Следующий() Цикл
			Строка.Параметры["Субконто" + ВидыСубконто.НомерСтроки] = ?(ВидыСубконто.ТолькоОбороты, "(об) ", "")+ВидыСубконто.Наименование;
		КонецЦикла;
			
		ТабДокумент.Вывести(Строка);
		
		Если ВыводитьОписания Тогда
			Попытка
				Описание=Макет.ПолучитьОбласть(ПланыСчетов[Выборка.Ссылка.Метаданные().Имя].ПолучитьИмяПредопределенного(Выборка.Ссылка));
				ТабДокумент.Вывести(Описание);
			Исключение
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
	ТабДокумент.ФиксацияСверху=2;
	
	Возврат ТабДокумент; 	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "Динамический список"

&НаКлиенте
Процедура ДинамическийСписок_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)	
	ДинамическиеСпискиКлиент.Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДинамическийСписок_ВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если ТипЗнч(Значение)=Тип("Массив") Тогда
		ОповеститьОВыборе(Значение[0]);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДинамическиеСпискиСервер.НастройкаПоПравилам(ДинамическийСписок, ЭтаФорма);	
	Элементы.ДинамическийСписок.РежимВыбора=Параметры.РежимВыбора;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДинамическиеСпискиКлиент.ПриОткрытии(Отказ, ДинамическийСписок, ЭтаФорма);
КонецПроцедуры