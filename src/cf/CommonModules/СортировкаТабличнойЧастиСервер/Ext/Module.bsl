Функция СформироватьКомпоновщикНастроек(ИмяТабличнойЧасти, Знач Ссылка, Знач УникальныйИдентификатор) Экспорт
	СхемаКомпоновкиДанных=Новый СхемаКомпоновкиДанных; стрЗапрос="";

	мдОбъект=Ссылка.Метаданные();
	Для каждого СтрокаКоллекции Из мдОбъект.ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты Цикл
		Если СтрокаКоллекции.Имя="ID" Тогда Продолжить; КонецЕсли; 
		стрЗапрос=стрЗапрос+?(стрЗапрос="", "Выбрать", ",")+Символы.ПС+"ИсточникДанных."+СтрокаКоллекции.Имя;
	КонецЦикла;	
	стрЗапрос=стрЗапрос+Символы.ПС+"ИЗ "+мдОбъект.ПолноеИмя()+"."+ИмяТабличнойЧасти+" Как ИсточникДанных";	
	
	ИсточникДанных=СхемаКомпоновкиДанных.ИсточникиДанных.Добавить(); 
	ИсточникДанных.Имя="ИсточникДанных";
	ИсточникДанных.СтрокаСоединения="";
	ИсточникДанных.ТипИсточникаДанных="Local";
	
	НаборДанных=СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя="НаборДанных";
	НаборДанных.ИсточникДанных="ИсточникДанных";
	НаборДанных.АвтоЗаполнениеДоступныхПолей=Истина;
	НаборДанных.Запрос=стрЗапрос;

	АдресСхемыКомпоновкиДанных=ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроек=Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));

	Возврат КомпоновщикНастроек;
КонецФункции

Процедура Сортировать(ИмяТабличнойЧасти, стрСортировка, Объект) Экспорт
	Запрос=Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсточникДанных", Объект[ИмяТабличнойЧасти].Выгрузить());
	Запрос.Текст="
	|Выбрать *
	|ПОМЕСТИТЬ ИсточникДанных
	|ИЗ &ИсточникДанных Как ИсточникДанных
	|;
	|ВЫБРАТЬ *	
	|ИЗ
	|	ИсточникДанных Как ИсточникДанных
	|УПОРЯДОЧИТЬ ПО "+стрСортировка+"
	|";
	Объект[ИмяТабличнойЧасти].Загрузить(Запрос.Выполнить().Выгрузить());

	Запрос.Текст="УНИЧТОЖИТЬ ИсточникДанных";
	Запрос.Выполнить();	
КонецПроцедуры
