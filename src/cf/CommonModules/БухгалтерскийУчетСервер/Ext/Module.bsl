// Получает сумму списания активов для заполнения печатных форм документов
//
Функция ПолучитьСуммуСписанияАктивов(ДокументСсылка, СвернутьПоСчету=Ложь, СвернутьПоНомеруСтроки=Ложь) Экспорт
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	Запрос.УстановитьПараметр("СчетГТД", ПланыСчетов.Хозрасчетный.ГТД);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	ИсточникДанных.СчетДт КАК Счет,
	|	ВЫБОР
	|		КОГДА ИсточникДанных.СубконтоКт1 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ИсточникДанных.СубконтоКт1
	|		КОГДА ИсточникДанных.СубконтоКт2 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ИсточникДанных.СубконтоКт2
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ИсточникДанных.СубконтоКт1 ССЫЛКА Справочник.Склады
	|			ТОГДА ИсточникДанных.СубконтоКт1
	|		КОГДА ИсточникДанных.СубконтоКт2 ССЫЛКА Справочник.Склады
	|			ТОГДА ИсточникДанных.СубконтоКт2
	|	КОНЕЦ КАК Склад,
	|	ИсточникДанных.НомерСтрокиТабличнойЧасти,
	|	СУММА(ИсточникДанных.КоличествоКт) КАК Количество,
	|	СУММА(ИсточникДанных.Сумма) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор = &Регистратор И (НЕ СчетКт = &СчетГТД)) КАК ИсточникДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсточникДанных.СчетДт,
	|	ИсточникДанных.СубконтоКт1,
	|	ИсточникДанных.СубконтоКт2,
	|	ИсточникДанных.НомерСтрокиТабличнойЧасти
	|
	|";		
	тзРезультат=Запрос.Выполнить().Выгрузить();
	тзРезультат.Свернуть("Номенклатура, Склад"+?(СвернутьПоСчету, ", Счет", "")+?(СвернутьПоНомеруСтроки, ", НомерСтрокиТабличнойЧасти", ""), "Количество, Сумма");
	Возврат тзРезультат;
КонецФункции

Процедура УстановитьПодразделениеПроводки(Проводка, Подразделение, ДтКт = "Дт") Экспорт
	Если Проводка["Счет"+ДтКт].УчетПоПодразделениям Тогда
		Проводка["Подразделение" + ДтКт]=Подразделение;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьПодразделенияПроводки(Проводка, ПодразделениеДт, ПодразделениеКт) Экспорт
	УстановитьПодразделениеПроводки(Проводка, ПодразделениеДт, "Дт");	
	УстановитьПодразделениеПроводки(Проводка, ПодразделениеКт, "Кт");
КонецПроцедуры

Процедура УстановитьСубконтоПоСчету(Счет, Субконто, ИмяСубконто, ЗначениеСубконто, Сообщать = Ложь, Заголовок = "", ВидыСубконтоСчета = Неопределено) Экспорт

	Если Счет = Неопределено ИЛИ Счет.Пустая() Тогда Возврат; КонецЕсли;

	Если ВидыСубконтоСчета = Неопределено Тогда
	     ВидыСубконтоСчета = Счет.ВидыСубконто;
	КонецЕсли; 
	
	Если ТипЗнч(ИмяСубконто) = Тип("Число") Тогда
		Если ИмяСубконто > ВидыСубконтоСчета.Количество() Тогда Возврат; КонецЕсли;
		ВидСубк = ВидыСубконтоСчета[ИмяСубконто - 1].ВидСубконто;
	Иначе
		ВидСубк = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные[ИмяСубконто];
		Если ВидыСубконтоСчета.Найти(ВидСубк) = Неопределено Тогда
			Если Сообщать тогда
				Сообщить("Вид субконто <" + ВидСубк + "> для счета """+Счет.Код +" ("+Счет.Наименование+")"" не определен.");
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ВидСубк.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто)) Тогда
		Если НЕ ЗначениеЗаполнено(ЗначениеСубконто) Тогда
			Сообщить("На счете "+Счет+"  Не заполнено субконто "+ВидСубк);
		КонецЕсли;	
		Субконто.Вставить(ВидСубк, ЗначениеСубконто);

	ИначеЕсли Сообщать тогда
		Сообщить("Неверное значение """ + ЗначениеСубконто + """ для вида субконто <" + ВидСубк + ">");
	КонецЕсли;

КонецПроцедуры

Функция ТаблицаСубконтоПоСчету(Счет, СтруктураСубконто) Экспорт
	ЗначениеСубконто1=СтруктураСубконто.ЗначениеСубконто1;
	ЗначениеСубконто2=СтруктураСубконто.ЗначениеСубконто2;
	ЗначениеСубконто3=СтруктураСубконто.ЗначениеСубконто3;
	
	тзДанные=Новый ТаблицаЗначений;
	тзДанные.Колонки.Добавить("ВидСубконто");
	тзДанные.Колонки.Добавить("ТипЗначения");
	тзДанные.Колонки.Добавить("Значение");
	//тзДанные.Колонки.Добавить("СвязиПараметровВыбора");

	Для каждого СтрокаКоллекции Из Счет.ВидыСубконто Цикл
		НоваяСтрока=тзДанные.Добавить();
		НоваяСтрока.ВидСубконто=СтрокаКоллекции.ВидСубконто;
		НоваяСтрока.ТипЗначения=СтрокаКоллекции.ВидСубконто.ТипЗначения;
		Если НЕ ЗначениеСубконто1=Неопределено Тогда
			НоваяСтрока.Значение=?(ЗначениеСубконтоСоответствуетТипу(ЗначениеСубконто1, НоваяСтрока.ТипЗначения), ЗначениеСубконто1, Неопределено);
		КонецЕсли;
		Если НЕ ЗначениеСубконто2=Неопределено Тогда
			НоваяСтрока.Значение=?(ЗначениеСубконтоСоответствуетТипу(ЗначениеСубконто2, НоваяСтрока.ТипЗначения), ЗначениеСубконто2, Неопределено);
		КонецЕсли;
		Если НЕ ЗначениеСубконто3=Неопределено Тогда
			НоваяСтрока.Значение=?(ЗначениеСубконтоСоответствуетТипу(ЗначениеСубконто3, НоваяСтрока.ТипЗначения), ЗначениеСубконто3, Неопределено);
		КонецЕсли;
	КонецЦикла;

	Возврат тзДанные;
КонецФункции

Функция ЗначениеСубконтоСоответствуетТипу(ЗначениеСубконто, ТипЗначения)
	ТекущийТип=ТипЗнч(ЗначениеСубконто);
	Для каждого Тип Из ТипЗначения.Типы() Цикл
		Если ТекущийТип=Тип Тогда Возврат Истина; КонецЕсли; 
	КонецЦикла;
	Возврат Ложь;
КонецФункции