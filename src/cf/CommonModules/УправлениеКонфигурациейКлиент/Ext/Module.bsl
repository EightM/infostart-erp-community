Процедура ОткрытьРегламентированныеОтчеты() Экспорт
	ОткрытьФорму("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.УправлениеОтчетностью");
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылку(НавигационнаяСсылка, Знач Оповещение = Неопределено) Экспорт
	Контекст = Новый Структура;
	Контекст.Вставить("НавигационнаяСсылка", НавигационнаяСсылка);
	Контекст.Вставить("Оповещение", Оповещение);
	ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось перейти по ссылке ""%1"" по причине: 
		|Неверно задана навигационная ссылка.'"), НавигационнаяСсылка);
	Если Не СтрНачинаетсяС(НавигационнаяСсылка, "e1cib/") И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "http:") И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "https:")
		И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "e1c:") И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "v8help:") И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "mailto:")
		И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "tel:") И НЕ СтрНачинаетсяС(НавигационнаяСсылка, "skype:") Тогда
		
		ОткрытьНавигационнуюСсылкуОповеститьОбОшибке(ОписаниеОшибки, Контекст);
		Возврат;
	КонецЕсли;
	Если СтрНачинаетсяС(НавигационнаяСсылка, "http://") Или СтрНачинаетсяС(НавигационнаяСсылка, "https://")
		Или СтрНачинаетсяС(НавигационнаяСсылка, "e1c:") Или СтрНачинаетсяС(НавигационнаяСсылка, "e1cib/") Тогда 
		Попытка
#Если ТолстыйКлиентОбычноеПриложение Тогда
			// Особенность платформы: ПерейтиПоНавигационнойСсылке не доступен в толстом клиенте обычного приложения.
			Оповещение = Новый ОписаниеОповещения(,, Контекст, "ОткрытьНавигационнуюСсылкуПриОбработкеОшибки", ЭтотОбъект);
			НачатьЗапускПриложения(Оповещение, НавигационнаяСсылка);
#Иначе
			ПерейтиПоНавигационнойСсылке(НавигационнаяСсылка);
#КонецЕсли
		Исключение
			ОткрытьНавигационнуюСсылкуОповеститьОбОшибке(ОписаниеОшибки, Контекст);
			Возврат;
		КонецПопытки;
		Если Оповещение <> Неопределено Тогда 
			ПриложениеЗапущено = Истина;
			ВыполнитьОбработкуОповещения(Оповещение, ПриложениеЗапущено);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	Если СтрНачинаетсяС(НавигационнаяСсылка, "v8help://") Тогда 
		//*** ОткрытьСправку(НавигационнаяСсылка);
		Возврат;
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("ОткрытьНавигационнуюСсылкуПослеПроверкиРасширенияРаботыСФайлами", ЭтотОбъект, Контекст);
	ТекстПредложения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Для открытия ссылки ""%1"" необходимо установить расширение работы с файлами.'"), НавигационнаяСсылка);
	ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, ТекстПредложения, Ложь);
КонецПроцедуры

Процедура ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещенияОЗакрытии, ТекстПредложения = "", ВозможноПродолжениеБезУстановки = Истина) Экспорт
	ОписаниеОповещенияЗавершение = Новый ОписаниеОповещения("ПоказатьВопросОбУстановкеРасширенияРаботыСФайламиЗавершение", ЭтотОбъект, ОписаниеОповещенияОЗакрытии);
#Если ВебКлиент Тогда
	ВызватьИсключение("Подключение расширения не поддерживается в Веб-клиенте!");
#КонецЕсли
	// В мобильном, тонком и толстом клиентах расширение подключено всегда.
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияЗавершение, "ПодключениеНеТребуется");
КонецПроцедуры

Процедура ПоказатьВопросОбУстановкеРасширенияРаботыСФайламиЗавершение(Действие, ОповещениеОЗакрытии) Экспорт
	РасширениеПодключено = (Действие = "РасширениеПодключено" Или Действие = "ПодключениеНеТребуется");
	ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, РасширениеПодключено);
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПослеПроверкиРасширенияРаботыСФайлами(РасширениеПодключено, Контекст) Экспорт
	НавигационнаяСсылка = Контекст.НавигационнаяСсылка;
	Если РасширениеПодключено Тогда
		Оповещение          = Контекст.Оповещение;
		ДождатьсяЗавершения = (Оповещение <> Неопределено);
		Оповещение = Новый ОписаниеОповещения("ОткрытьНавигационнуюСсылкуПослеЗапускаПриложения", ЭтотОбъект, Контекст, "ОткрытьНавигационнуюСсылкуПриОбработкеОшибки", ЭтотОбъект);
		НачатьЗапускПриложения(Оповещение, НавигационнаяСсылка,, ДождатьсяЗавершения);
	Иначе
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Расширение для работы с файлами не установлено, переход по ссылке ""%1"" невозможен.'"), НавигационнаяСсылка);
		ОткрытьНавигационнуюСсылкуОповеститьОбОшибке(ОписаниеОшибки, Контекст);
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПослеЗапускаПриложения(КодВозврата, Контекст) Экспорт 
	Оповещение = Контекст.Оповещение;
	Если Оповещение <> Неопределено Тогда 
		ПриложениеЗапущено = (КодВозврата = 0 Или КодВозврата = Неопределено);
		ВыполнитьОбработкуОповещения(Оповещение, ПриложениеЗапущено);
	КонецЕсли;
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПриОбработкеОшибки(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт 
	СтандартнаяОбработка = Ложь;
	ОткрытьНавигационнуюСсылкуОповеститьОбОшибке("", Контекст);
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуОповеститьОбОшибке(ОписаниеОшибки, Контекст) Экспорт
	Оповещение = Контекст.Оповещение;
	Если Оповещение = Неопределено Тогда
		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда 
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	Иначе 
		ПриложениеЗапущено = Ложь;
		ВыполнитьОбработкуОповещения(Оповещение, ПриложениеЗапущено);
	КонецЕсли;
КонецПроцедуры
