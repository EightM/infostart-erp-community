////////////////////////////////////////////////////////////////////////////////
//Управление спецификацией

Функция ПолучитьСпецификациюНаДату(ВариантСпецификации, ХарактеристикаНоменклатуры=Неопределено, НаДату=Неопределено) Экспорт
	Если ВариантСпецификации.Пустая() Тогда Возврат Неопределено; КонецЕсли; 

	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Спецификация", ВариантСпецификации);
	Запрос.УстановитьПараметр("Дата", КонецДня(?(НаДату=Неопределено, ТекущаяДата(), НаДату)));
	Если ЗначениеЗаполнено(ХарактеристикаНоменклатуры) Тогда
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", ХарактеристикаНоменклатуры);
	КонецЕсли;
	Запрос.Текст="
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсточникДанных.Ссылка КАК Документ
	|ИЗ
	|	Документ.Спецификация КАК ИсточникДанных
	|ГДЕ
	|   ИсточникДанных.Дата <= &Дата
	| 	И ИсточникДанных.Спецификация = &Спецификация
	| 	"+?(ЗначениеЗаполнено(ХарактеристикаНоменклатуры), "И ИсточникДанных.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры", "")+"
	| 	И ИсточникДанных.Проведен 
	|УПОРЯДОЧИТЬ ПО
	|	ИсточникДанных.Дата УБЫВ
	|";
	Выборка=Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Документ, Неопределено);
КонецФункции

Функция ПолучитьВариантСпецификации(СпецификацияСсылка, НоменклатураСсылка, Сообщить=Ложь) Экспорт
	Если Не СпецификацияСсылка.Пустая() Тогда Возврат СпецификацияСсылка; КонецЕсли;

	Спецификация=НоменклатураСсылка.ОсновнаяСпецификация;
	Если Спецификация.Пустая() Тогда
		ВыборкаСпецификации=Справочники.СпецификацияНоменклатуры.Выбрать(,НоменклатураСсылка,,"Код Возр");
		Если Не ВыборкаСпецификации.Следующий() Тогда
			Если Сообщить Тогда
				#Если Клиент Тогда
				Сообщить("Изделие <"+СокрЛП(НоменклатураСсылка)+"> не имеет спецификаций!");
				#КонецЕсли
			КонецЕсли;
			Возврат Неопределено;
		КонецЕсли;
		Спецификация=ВыборкаСпецификации.Ссылка;
	КонецЕсли;
	
	Возврат Спецификация;
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("Спецификация", "Спецификация по комплектующим");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция ПолучитьСпецификациюНоменклатуры(НаДату, Номенклатура)
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.Текст="
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокСпецификация.Дата,
	|	ДокСпецификация.Спецификация,
	|	ДокСпецификация.Номенклатура,
	|	ДокСпецификация.Номенклатура.Артикул КАК Артикул,
	|	ДокСпецификация.Комплектующие.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Номенклатура.Артикул КАК Артикул,
	|		ХарактеристикаНоменклатуры,
	|		СтатьяЗатрат,
	|		Количество,
	|		ЕдиницаИзмерения,
	|		ДоляСтоимости,
	|		Потери,
	|		СпецификацияКомплектующей,
	|		НормаРасхода,
	|		ОкруглятьДоКоличествоЗнаков,
	|		ВариантИспользованияВПроизводственномЦикл
	|	)
	|ИЗ
	|	Документ.Спецификация КАК ДокСпецификация
	|ГДЕ
	|	ДокСпецификация.Дата <= &Дата
	|	И ДокСпецификация.Номенклатура = &Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокСпецификация.Дата УБЫВ
	|";
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка;
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция Печать_Спецификация(СтруктураПараметров)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;

	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.Текст="ВЫБРАТЬ
	             |	ДокСпецификация.Дата,
	             |	ДокСпецификация.Спецификация,
	             |	ДокСпецификация.Номенклатура,
	             |	ДокСпецификация.Номенклатура.Артикул КАК Артикул,
	             |	ДокСпецификация.ПоследнийАвтор КАК ПослАвтор,
	             |	ДокСпецификация.Комплектующие.(
	             |		Ссылка,
	             |		НомерСтроки,
	             |		Номенклатура,
	             |		Номенклатура.Артикул КАК Артикул,
	             |		ХарактеристикаНоменклатуры,
	             |		СтатьяЗатрат,
	             |		Количество,
	             |		ЕдиницаИзмерения,
	             |		ДоляСтоимости,
	             |		Потери,
	             |		СпецификацияКомплектующей,
	             |		НормаРасхода,
	             |		ОкруглятьДоКоличествоЗнаков,
	             |		ВариантИспользованияВПроизводственномЦикл
	             |	)
	             |ИЗ
	             |	Документ.Спецификация КАК ДокСпецификация
				 |ГДЕ
				 |	ДокСпецификация.Ссылка=&Ссылка";
		 
	Шапка=Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	тзКомплектующие=Шапка.Комплектующие.Выгрузить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Спецификация";

	Макет = ИнициализацияМакета(СтруктураПараметров, "Спецификация");
    ОбластьШапки=Макет.ПолучитьОбласть("Шапка");
	ОбластьШапки.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьШапки);
	
	Н=1;
	Для каждого Строка Из тзКомплектующие Цикл	
		ОбластьМакета1=Макет.ПолучитьОбласть("Область1");
		ОбластьМакета1.Параметры.Заполнить(Строка);
		ОбластьМакета1.Параметры.Н=Н;
		ТабДокумент.Вывести(ОбластьМакета1);
		Н=Н+1;
		
		СпецификацияКомплектующей=ПолучитьСпецификациюНоменклатуры(Шапка.Дата, Строка.Номенклатура);
		Если СпецификацияКомплектующей="" Тогда Продолжить; КонецЕсли;
		тзКомплектующие1=СпецификацияКомплектующей.Комплектующие.Выгрузить();		
		Н1=1;
		Для каждого Строка1 Из тзКомплектующие1 Цикл
			ОбластьМакета2=Макет.ПолучитьОбласть("Область2");
			ОбластьМакета2.Параметры.Заполнить(Строка1);
			ОбластьМакета2.Параметры.Н=Н1;
			ТабДокумент.Вывести(ОбластьМакета2);
			Н1=Н1+1;		
			СпецификацияКомплектующей=ПолучитьСпецификациюНоменклатуры(Шапка.Дата, Строка1.Номенклатура);
			Если СпецификацияКомплектующей="" Тогда Продолжить; КонецЕсли;
			тзКомплектующие2=СпецификацияКомплектующей.Комплектующие.Выгрузить();
			Н2=1;
			Для каждого Строка2 Из тзКомплектующие2 Цикл
				ОбластьМакета3=Макет.ПолучитьОбласть("Область3");
				ОбластьМакета3.Параметры.Заполнить(Строка2);
				ОбластьМакета3.Параметры.Н=Н2;
				ТабДокумент.Вывести(ОбластьМакета3);
				Н2=Н2+1;
			КонецЦикла;			
		КонецЦикла; 
	КонецЦикла; 
	
	ОбластьПодвала=Макет.ПолучитьОбласть("Подвал");
	ОбластьПодвала.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьПодвала);
	
	Возврат ТабДокумент;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="Спецификация" Тогда
		ТабДокумент=Печать_Спецификация(СтруктураПараметров);
	КонецЕсли;
    Возврат ТабДокумент;
КонецФункции
