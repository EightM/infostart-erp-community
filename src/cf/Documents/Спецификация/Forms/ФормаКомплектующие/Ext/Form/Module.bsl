&НаСервереБезКонтекста
Функция СформироватьСтруктураТабличнойЧасти(ИмяТабличнойЧасти)
	Структура=Новый Структура;
	Для каждого мдРеквизит Из Метаданные.Документы.Спецификация.ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты Цикл
		Структура.Вставить(мдРеквизит.Имя);
	КонецЦикла; 
	Возврат Структура;
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	СтруктураВозврата=СформироватьСтруктураТабличнойЧасти("Комплектующие");
	СтруктураВозврата.Вставить("ИмяТЧ", ИмяТЧ);
	СтруктураВозврата.Вставить("Редактирование", Редактирование);
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ЭтаФорма);
	ЭтаФорма.Закрыть(СтруктураВозврата);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="Номенклатура" Тогда
		СтруктураНоменклатуры=ОбщегоНазначенияСервер.ЗначенияРеквизитовОбъекта(Номенклатура, "ЕдиницаХраненияОстатков,СтатьяЗатрат");
		ЕдиницаИзмерения=СтруктураНоменклатуры.ЕдиницаХраненияОстатков;
		СтатьяЗатрат=СтруктураНоменклатуры.СтатьяЗатрат;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Редактирование=Ложь;
	Количество=0;
	Если Не Параметры.ДанныеСтроки=Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДанныеСтроки);
		Редактирование=Истина;
	КонецЕсли; 
	Если Не Параметры.Изделие=Неопределено Тогда
		Изделие=Параметры.Изделие;
	КонецЕсли; 
	Если ТипЗнч(Количество)=Тип("Строка") Тогда
		ВычислятьНормуРасходаПоФормуле=Истина;
	КонецЕсли;
	ИмяТЧ=Параметры.ИмяТЧ;
	Элементы.ВычислятьНормуРасходаПоФормуле.Доступность=Не ТолькоПросмотр;
КонецПроцедуры

&НаКлиенте
Процедура ВычислятьНормуРасходаПоФормулеПриИзменении(Элемент)
	Количество=?(ВычислятьНормуРасходаПоФормуле, "", 0);
КонецПроцедуры