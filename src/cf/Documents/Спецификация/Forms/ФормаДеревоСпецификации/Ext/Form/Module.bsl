&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="Обновить" Тогда
		дзСпецификации_Обновить();
		
	ИначеЕсли Команда.Имя="Печать" Тогда
		ВывестиНаПечать();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Функция СформироватьСписокХарактеристикИзделия(СпецификацияСсылка)
	спХарактеристики=Новый СписокЗначений;
	спХарактеристики.Добавить(Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка(), "Характеристика <<неопределена>>");
	Если ГруппироватьПоХарактеристикам Тогда
		ВыбокаХарактеристик=Справочники.ХарактеристикиНоменклатуры.Выбрать(,СпецификацияСсылка.Владелец.Ссылка);
		Пока ВыбокаХарактеристик.Следующий() Цикл
			Если ВыбокаХарактеристик.ПометкаУдаления Тогда Продолжить; КонецЕсли;
			спХарактеристики.Добавить(ВыбокаХарактеристик.Ссылка, "Характеристика <<"+СокрЛП(ВыбокаХарактеристик.Наименование)+">>");
		КонецЦикла;
	КонецЕсли;
	Возврат спХарактеристики;
КонецФункции

&НаСервере
Функция СформироватьСписокРазделовЗатрат()
	спРазделыЗатрат=Новый СписокЗначений;
	ВыборкаРазделовЗатрат=Справочники.КлассификаторПеречислений.Выбрать(Справочники.КлассификаторПеречислений.РазделыЗатрат);
	Пока ВыборкаРазделовЗатрат.Следующий() Цикл
		спРазделыЗатрат.Добавить(ВыборкаРазделовЗатрат.Ссылка);
	КонецЦикла;
	Возврат спРазделыЗатрат;
КонецФункции

&НаСервере
Процедура дзСпецификации_Сформировать(дзСсылка, ДокументСпецификация)
	спХарактеристики=СформироватьСписокХарактеристикИзделия(ДокументСпецификация.Спецификация);

	Для Каждого СтрокаКоллекцииХарактеристики Из спХарактеристики Цикл
		текХарактеристикаИзделия=СтрокаКоллекцииХарактеристики.Значение;
		
		ВеткаХарактеристики=дзСсылка;

		ДобавитьВеткуХарактеристики=?(ИсключитьПустуюХарактеристику И текХарактеристикаИзделия.Пустая(), Ложь, Истина);
		Если ДобавитьВеткуХарактеристики Тогда
			ВеткаХарактеристики=ВеткаХарактеристики.Строки.Добавить();
			ВеткаХарактеристики.Объект=текХарактеристикаИзделия;
			ВеткаХарактеристики.Объект=СтрокаКоллекцииХарактеристики.Представление;
			ВеткаХарактеристики.ХарактеристикаНоменклатуры=текХарактеристикаИзделия;
			ВеткаХарактеристики.Спецификация=ДокументСпецификация.Спецификация;
			ВеткаХарактеристики.ЭтоГруппа=Истина;
			ВеткаХарактеристики.ИндексКартинки=5;
		КонецЕсли; 

		//Комплектующие
		ЕстьВеткаКомплектующие=Ложь; ВеткаКомплектующие=ВеткаХарактеристики;
		Для каждого ВыборкаКомплектующих Из ДокументСпецификация.Комплектующие Цикл
			Если ГруппироватьПоХарактеристикам Тогда
				Если НЕ ДокументСпецификация.ХарактеристикаНоменклатуры=текХарактеристикаИзделия Тогда
					Продолжить; 
				КонецЕсли;
			КонецЕсли;

			Если ГруппироватьПоКомплектующим Тогда
				Если ЕстьВеткаКомплектующие=Ложь Тогда
					ЕстьВеткаКомплектующие=Истина;
					ВеткаКомплектующие=ВеткаХарактеристики.Строки.Добавить();
					ВеткаКомплектующие.Объект="Комплектующие";
					ВеткаКомплектующие.ХарактеристикаНоменклатуры=ВыборкаКомплектующих.ХарактеристикаНоменклатуры;
					ВеткаКомплектующие.Спецификация=ДокументСпецификация.Спецификация;
					ВеткаКомплектующие.ЭтоГруппа=Истина;
					ВеткаКомплектующие.ИндексКартинки=4;
					ВеткаКомплектующие.ТипЗаписи="Комплектующая";
				КонецЕсли;
			КонецЕсли; 

			НоваяСтрока=ВеткаКомплектующие.Строки.Добавить();
			НоваяСтрока.Объект=ВыборкаКомплектующих.Номенклатура;
			//****** НоваяСтрока.ХарактеристикаКомплектующей=ВыборкаКомплектующих.ХарактеристикаНоменклатуры;
			НоваяСтрока.ХарактеристикаНоменклатуры =ВыборкаКомплектующих.ХарактеристикаНоменклатуры;
			НоваяСтрока.Количество=?((ВыборкаКомплектующих.НормаРасхода=1) ИЛИ (ВыборкаКомплектующих.НормаРасхода=0),ВыборкаКомплектующих.Количество,ВыборкаКомплектующих.Количество/ВыборкаКомплектующих.НормаРасхода);
			НоваяСтрока.Единица=ВыборкаКомплектующих.ЕдиницаИзмерения;
			НоваяСтрока.Спецификация=ДокументСпецификация.Спецификация;
			НоваяСтрока.СтатьяЗатрат=ВыборкаКомплектующих.СтатьяЗатрат;
			НоваяСтрока.ТипЗаписи="Комплектующая";

			Если Не ТипЦен_Комплектующие.Пустая() Тогда
				НоваяСтрока.Цена=ЦенообразованиеСервер.ПолучитьЦенуНоменклатуры(НоваяСтрока.Объект, НоваяСтрока.ХарактеристикаНоменклатуры, ТипЦен_Комплектующие, Дата);
				НоваяСтрока.Стоимость=НоваяСтрока.Количество*НоваяСтрока.Цена;
			КонецЕсли; //***	

			Если ПоказатьОстаток Тогда
				НоваяСтрока.Остаток=УправлениеПроведением.УчетПартийТМЦ_ПолучитьОстаток(НоваяСтрока.Объект, НоваяСтрока.ХарактеристикаНоменклатуры, , спСклады);
			КонецЕсли;

			СпецификацияКомплектующего=ВыборкаКомплектующих.СпецификацияКомплектующей;
			Если СпецификацияКомплектующего.Пустая() Тогда
				СпецификацияКомплектующего=ВыборкаКомплектующих.Номенклатура.ОсновнаяСпецификация;
			КонецЕсли;

			НоваяСтрока.ИндексКартинки=?(СпецификацияКомплектующего.Пустая(), 2, 0);
			Если НоваяСтрока.ИндексКартинки=0 Тогда // Продукт (полуфабрикат)
				ДокСсылка = Документы.Спецификация.ПолучитьСпецификациюНаДату(СпецификацияКомплектующего, ,Дата);
				Если ДокСсылка=Неопределено Тогда Продолжить; КонецЕсли; 
				НоваяСтрока.ЭтоГруппа=Истина;
				НоваяСтрока.Документ=ДокСсылка;
				дзСпецификации_Сформировать(НоваяСтрока, ДокСсылка);
			КонецЕсли;
			
			Если НЕ НоваяСтрока.ИндексКартинки=0 Тогда
				Если Не ТипЦен_Комплектующие.Пустая() И НоваяСтрока.Цена=0 Тогда
					Сообщить("Комплектующая {"+СокрЛП(НоваяСтрока.Объект)+"} не имеет цены");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		
		//Статьи затрат	
		Если ДокументСпецификация.Операции.Количество()>0 Тогда
			тзСтатьиЗатрат = ДокументСпецификация.Операции.Выгрузить(,);
			тзСтатьиЗатрат.Колонки.Добавить("РазделЗатрат"); 
			Для Каждого СтрокаКоллекции Из тзСтатьиЗатрат Цикл
				Если ГруппироватьПоХарактеристикам Тогда
					Если НЕ ДокументСпецификация.ХарактеристикаНоменклатуры=текХарактеристикаИзделия Тогда
						Продолжить; 
					КонецЕсли;				
				КонецЕсли;
				СтрокаКоллекции.РазделЗатрат=СтрокаКоллекции.СтатьяЗатрат.РазделЗатрат;
			КонецЦикла;  

			ЕстьВеткаЗатрат=Ложь; ВеткаСтатейЗатрат=ВеткаХарактеристики;
			спРазделыЗатрат=СформироватьСписокРазделовЗатрат();
			Для Каждого СтрокаКоллекции Из спРазделыЗатрат Цикл
				РезультатПоиска=тзСтатьиЗатрат.НайтиСтроки(Новый Структура("РазделЗатрат", СтрокаКоллекции.Значение));
				Если РезультатПоиска.Количество()=0 Тогда Продолжить; КонецЕсли;
				
				Если ГруппироватьПоСтатьямЗатрат Тогда
					Если ЕстьВеткаЗатрат=Ложь Тогда
						ЕстьВеткаЗатрат=Истина;
						ВеткаСтатейЗатрат=ВеткаХарактеристики.Строки.Добавить();
						ВеткаСтатейЗатрат.Объект="Статьи затрат";
						ВеткаСтатейЗатрат.Спецификация=ДокументСпецификация.Спецификация;
						ВеткаСтатейЗатрат.ЭтоГруппа=Истина;
						ВеткаСтатейЗатрат.ТипЗаписи="СтатьяЗатрат";
						ВеткаСтатейЗатрат.ИндексКартинки=6;
					КонецЕсли;
				КонецЕсли;
				ВеткаРазделаЗатрат=ВеткаСтатейЗатрат;

				Если ГруппироватьПоРазделамЗатрат Тогда
					ВеткаРазделаЗатрат=ВеткаСтатейЗатрат.Строки.Добавить();
					ВеткаРазделаЗатрат.Объект=СтрокаКоллекции.Значение;
					ВеткаРазделаЗатрат.Спецификация=ДокументСпецификация.Спецификация;
					ВеткаРазделаЗатрат.ЭтоГруппа=Истина;
					ВеткаРазделаЗатрат.ТипЗаписи="СтатьяЗатрат";
					ВеткаРазделаЗатрат.ИндексКартинки=6;
				КонецЕсли;

				Для ъ=0 По РезультатПоиска.Количество()-1 Цикл
					СтрокаТаблицы=РезультатПоиска[ъ];
					НоваяСтрока=ВеткаРазделаЗатрат.Строки.Добавить();
					НоваяСтрока.Объект=СтрокаТаблицы.СтатьяЗатрат;
					НоваяСтрока.ХарактеристикаНоменклатуры=ДокументСпецификация.ХарактеристикаНоменклатуры;
					НоваяСтрока.Количество=СтрокаТаблицы.Количество;
					НоваяСтрока.Единица=СтрокаТаблицы.ЕдиницаИзмерения;
					НоваяСтрока.Цена=СтрокаТаблицы.Цена;
					НоваяСтрока.Стоимость=СтрокаТаблицы.Количество*СтрокаТаблицы.Цена;
					НоваяСтрока.Спецификация=ДокументСпецификация.Спецификация;
					НоваяСтрока.ТипЗаписи="СтатьяЗатрат";
					НоваяСтрока.ИндексКартинки=1;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		//Побочные продукты
		ЕстьВеткаПобочныеПродукты=Ложь; ВеткаПобочныеПродукты=ВеткаХарактеристики;
		Для каждого ВыборкаПобочныеПродукты Из ДокументСпецификация.ПобочныеПродукты Цикл
			Если ГруппироватьПоХарактеристикам Тогда
				Если НЕ ДокументСпецификация.ХарактеристикаНоменклатуры=текХарактеристикаИзделия Тогда
					Продолжить; 
				КонецЕсли;
			КонецЕсли;

			Если ГруппироватьПоПобочнымПродуктам Тогда
				Если ЕстьВеткаПобочныеПродукты=Ложь Тогда
					ЕстьВеткаПобочныеПродукты=Истина;
					ВеткаПобочныеПродукты=ВеткаХарактеристики.Строки.Добавить();
					ВеткаПобочныеПродукты.Объект="Побочные продукты";
					ВеткаПобочныеПродукты.ХарактеристикаНоменклатуры=ДокументСпецификация.ХарактеристикаНоменклатуры;
					ВеткаПобочныеПродукты.Спецификация=ДокументСпецификация.Спецификация;
					ВеткаПобочныеПродукты.ЭтоГруппа=Истина;
					ВеткаПобочныеПродукты.ТипЗаписи="ПобочныйПродукт";
					ВеткаПобочныеПродукты.ИндексКартинки=7;
				КонецЕсли;
			КонецЕсли; 

			НоваяСтрока=ВеткаПобочныеПродукты.Строки.Добавить();
			НоваяСтрока.Объект=ВыборкаПобочныеПродукты.ПобочныйПродукт;
			//НоваяСтрока.ХарактеристикаКомплектующей=ВыборкаПобочныеПродукты.ХарактеристикаПобочногоПродукта;
			//НоваяСтрока.ХарактеристикаНоменклатуры =ДокументСпецификация.ХарактеристикаНоменклатуры;
			НоваяСтрока.ХарактеристикаНоменклатуры =ВыборкаПобочныеПродукты.ХарактеристикаПобочногоПродукта;

			НоваяСтрока.Количество=ВыборкаПобочныеПродукты.Количество;
			НоваяСтрока.Единица=ВыборкаПобочныеПродукты.ЕдиницаИзмерения;
			НоваяСтрока.Спецификация=ДокументСпецификация.Спецификация;
			НоваяСтрока.СтатьяЗатрат=ВыборкаПобочныеПродукты.СтатьяЗатрат;
			
			Если Не ТипЦен_ПобочныеПродукты.Пустая() Тогда
				НоваяСтрока.Цена=ЦенообразованиеСервер.ПолучитьЦенуНоменклатуры(НоваяСтрока.Объект, НоваяСтрока.ХарактеристикаНоменклатуры, ТипЦен_ПобочныеПродукты, Дата);
				НоваяСтрока.Стоимость=НоваяСтрока.Количество*НоваяСтрока.Цена;
			КонецЕсли; //***	

			Если ПоказатьОстаток Тогда
				НоваяСтрока.Остаток=УправлениеПроведением.УчетПартийТМЦ_ПолучитьОстаток(НоваяСтрока.Объект, НоваяСтрока.ХарактеристикаНоменклатуры, , спСклады);
			КонецЕсли;
			
			НоваяСтрока.ИндексКартинки=3;
            НоваяСтрока.ТипЗаписи="ПобочныйПродукт";
		КонецЦикла; 		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура дзСпецификации_Обновить()
	дзБуфер=РеквизитФормыВЗначение("дзСпецификация");
	дзБуфер.Строки.Очистить();
	дзСпецификации_Сформировать(дзБуфер, ДокументИсточник);	
	Если РассчитыватьИтоги Тогда
		РассчитыватьИтогиПоДереву(дзБуфер);
	КонецЕсли; 
	ЗначениеВРеквизитФормы(дзБуфер, "дзСпецификация");	
КонецПроцедуры 

&НаСервере
Процедура РассчитыватьИтогиПоДереву(Ветка)
	Если Ветка.Строки.Количество()=0 Тогда Возврат; КонецЕсли;
	Для каждого СтрокаКоллекции Из Ветка.Строки Цикл
		Если СтрокаКоллекции.Строки.Количество()=0 Тогда Продолжить; КонецЕсли;
		РассчитыватьИтогиПоДереву(СтрокаКоллекции);
		СтрокаКоллекции.Стоимость=СтрокаКоллекции.Строки.Итог("Стоимость");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Страницы_ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница.Имя="Страница1" Тогда
		Видимость=НЕ ТипЦен_Комплектующие.Пустая() ИЛИ ТипЦен_ПобочныеПродукты.Пустая();
		Элементы.дзСпецификацияЦена.Видимость=Видимость;
		Элементы.дзСпецификацияСтоимость.Видимость=Видимость;
		Элементы.дзСпецификацияОстаток.Видимость=ПоказатьОстаток;
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Печать

&НаСервере
Процедура ОбходДереваЗначений(дзОтчет, Таблица, Макет, ВыборкаСтрок, Уровень)
	Для Каждого ВыборкаЗапроса Из ВыборкаСтрок Цикл
		ВыводимаяОбласть=Макет.ПолучитьОбласть("Строка|Группировки");
		ВыводимаяОбласть.Параметры["ЗначениеГруппировки"+Уровень]=ВыборкаЗапроса.Объект;
		Таблица.Вывести(ВыводимаяОбласть, ВыборкаЗапроса.Уровень(),,Ложь);

		Для Каждого Колонка Из Элементы.дзСпецификация.ПодчиненныеЭлементы Цикл
			Если Колонка.Имя="дзСпецификацияОбъект" Тогда Продолжить; КонецЕсли; 
			Если НЕ Колонка.Видимость Тогда Продолжить; КонецЕсли; 
			ВыводимаяОбласть=Макет.ПолучитьОбласть("Строка|Колонка");
			ВыводимаяОбласть.Параметры.Значение=ВыборкаЗапроса[стрЗаменить(Колонка.Имя, "дзСпецификация", "")];
			Таблица.Присоединить(ВыводимаяОбласть);
		КонецЦикла; 
        ВысотаТаблицы=Таблица.ВысотаТаблицы; ИндексКолонки=Уровень+1;

		текОбласть=Таблица.Область(ВысотаТаблицы, ИндексКолонки, ВысотаТаблицы, 8);
		текОбласть.Объединить();
		текОбласть.ГраницаСверху=Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Точечная, 1);
		текОбласть.Расшифровка=ВыборкаЗапроса.Объект;

		Если НЕ ВыборкаЗапроса.Строки.Количество()=0 Тогда
			ОбходДереваЗначений(дзОтчет, Таблица, Макет, ВыборкаЗапроса.Строки, Уровень+1);
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СформироватьПечатнуюФорму()
	дзОтчет=РеквизитФормыВЗначение("дзСпецификация");
	Если дзОтчет.Строки.Количество()=0 Тогда Возврат Неопределено; КонецЕсли;
	
	Таблица=Новый ТабличныйДокумент;

	// Выводим шапку отчета
	Макет=Документы.Спецификация.ПолучитьМакет("ДеревоСецификации");
	ВыводимаяОбласть=Макет.ПолучитьОбласть("Шапка|Группировки");
	ВыводимаяОбласть.Параметры.ОписаниеПоказатели="Спецификация номенклатуры: "+СокрЛП(ДокументИсточник.Номенклатура);
	Таблица.Вывести(ВыводимаяОбласть);	
	
	// Выводим колонку группировок
	ВыводимаяОбласть=Макет.ПолучитьОбласть("Заголовок|Группировки");
	ВыводимаяОбласть.Параметры.ОписаниеГруппировок="Номенование";
	Таблица.Вывести(ВыводимаяОбласть);

	//Выводим колонки
	//*** Для Каждого Колонка Из дзОтчет.Колонки Цикл
	Для Каждого Колонка Из Элементы.дзСпецификация.ПодчиненныеЭлементы Цикл
		Если Колонка.Имя="дзСпецификацияОбъект" Тогда Продолжить; КонецЕсли; 
		Если НЕ Колонка.Видимость Тогда Продолжить; КонецЕсли; 
		ВыводимаяОбласть=Макет.ПолучитьОбласть("Заголовок|Колонка");
		ВыводимаяОбласть.Параметры.Колонка=Колонка.Заголовок;
		Таблица.Присоединить(ВыводимаяОбласть);
	КонецЦикла;

	//Оформление шапки
	Таблица.Область(1, 2, 1, Таблица.ШиринаТаблицы).Объединить();
	Таблица.Область(2, 2, 2, Таблица.ШиринаТаблицы).Объединить();
	Таблица.Область(3, 2, 3, Таблица.ШиринаТаблицы).Объединить();

	ОбходДереваЗначений(дзОтчет, Таблица, Макет, дзОтчет.Строки, 1);

	// Выводим подвал
	ВыводимаяОбласть=Макет.ПолучитьОбласть("Подвал|Группировки");
	Таблица.Вывести(ВыводимаяОбласть);

	Для Каждого Колонка Из Элементы.дзСпецификация.ПодчиненныеЭлементы Цикл
		Если Колонка.Имя="дзСпецификацияОбъект" Тогда Продолжить; КонецЕсли; 
		Если НЕ Колонка.Видимость Тогда Продолжить; КонецЕсли; 
		ВыводимаяОбласть=Макет.ПолучитьОбласть("Подвал|Колонка");
		Таблица.Присоединить(ВыводимаяОбласть);
	КонецЦикла;

	Возврат Таблица;
КонецФункции

&НаКлиенте
Процедура ВывестиНаПечать()
	Таблица=СформироватьПечатнуюФорму();
	Если Таблица=Неопределено Тогда
		ПоказатьПредупреждение(, "Нет данных для вывода на печать!", 30);
		Возврат;
	КонецЕсли;
	Таблица.ТолькоПросмотр=Истина;
	Таблица.ОриентацияСтраницы=ОриентацияСтраницы.Портрет;
	Таблица.Автомасштаб=Истина;
	Таблица.ФиксацияСверху=5;
	Таблица.ОтображатьГруппировки=Ложь;
	Таблица.ОтображатьЗаголовки=Ложь;
	Таблица.ОтображатьСетку=Ложь;
	Таблица.Показать();		
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("Спецификация", ДокументИсточник) Тогда
		Отказ=Истина; Возврат;
	КонецЕсли;
	Параметры.Свойство("Дата", Дата);
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата=ТекущаяДата();
	КонецЕсли;
	ИсключитьПустуюХарактеристику=Истина;
	ГруппироватьПоКомплектующим=Истина;
	РассчитыватьИтоги=Истина;

	дзСпецификации_Обновить();
КонецПроцедуры
