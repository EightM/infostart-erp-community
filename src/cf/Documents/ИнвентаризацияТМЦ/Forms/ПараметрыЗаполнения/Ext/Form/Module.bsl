&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="Применить" Тогда
		ЭтаФорма.Закрыть(СформироватьТаблицуРезультат());
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Функция СформироватьТаблицуРезультат()
	СхемаКомпоновкиДанных=ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);

	ПараметрыЗаполнения.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Период", Дата);

	КомпоновщикМакета=Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки=КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, ПараметрыЗаполнения.Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновкиДанных=Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);

	тзДанные=Новый ТаблицаЗначений;
	ПроцессорВывода=Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.ОтображатьПроцентВывода=Истина;
	ПроцессорВывода.УстановитьОбъект(тзДанные);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

	Возврат УниверсальныеМеханизмыСервер.ЗначениеВСтрокуXML(тзДанные);
КонецФункции

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	 ПараметрыЗаполнения_Инициализация();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "Параметры заполнения"

&НаСервере
Процедура ПараметрыЗаполнения_Инициализация()
	СхемаКомпоновкиДанных=Документы.ИнвентаризацияТМЦ.ПолучитьМакет(ВариантЗаполнения+"СКД");
	АдресСхемыКомпоновкиДанных=ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	ПараметрыЗаполнения.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	ПараметрыЗаполнения.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	ПараметрыЗаполнения_УстановитьЭлементОтбора("Организация", Организация);
	ПараметрыЗаполнения_УстановитьЭлементОтбора("Склад", Склад);	
	ПараметрыЗаполнения_УстановитьЭлементОтбора("КоличествоУчет", 0);
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗаполнения_УстановитьЭлементОтбора(стрПоле, Значение)
	ЭлементОтбора=ОтборыСписковКлиентСервер.НайтиЭлементОтбораПоЗначению(ПараметрыЗаполнения, стрПоле); 
	Если ЭлементОтбора=Неопределено Тогда
		ЭлементОтбора=ПараметрыЗаполнения.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение=Новый ПолеКомпоновкиДанных(стрПоле);		
	КонецЕсли;
	ЭлементОтбора.ВидСравнения=ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование=ЗначениеЗаполнено(Значение);
	ЭлементОтбора.ПравоеЗначение=Значение;
	Если стрПоле="КоличествоУчет" Тогда
		ЭлементОтбора.ВидСравнения=ВидСравненияКомпоновкиДанных.Больше;
		ЭлементОтбора.Использование=Истина;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Дата", Дата);
	Параметры.Свойство("Склад", Склад);
	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("ВариантЗаполнения", ВариантЗаполнения);
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок=Параметры.Заголовок;
	КонецЕсли;

	Если ПустаяСтрока(ВариантЗаполнения) Тогда ВариантЗаполнения="ОперативныйУчет"; КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Дата) Тогда Дата=ТекущаяДата(); КонецЕсли;

	ПараметрыЗаполнения_Инициализация();
КонецПроцедуры