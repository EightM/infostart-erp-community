Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("Расшифровка", "Расшифровка");
КонецПроцедуры

Функция Печать_Расшифровка(СтруктураПараметров)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;
	
	ТабДокумент=Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати="ПАРАМЕТРЫ_ПЕЧАТИ_Расшифровка";

	Макет=ИнициализацияМакета(СтруктураПараметров, "Расшифровка");

	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заголовок="Расшифровка распределения расходов";
	ТабДокумент.Вывести(ОбластьМакета);
	
	тзИсходная=СсылкаНаОбъект.Расшифровка.Выгрузить();
	
	тзКонтрагенты=СсылкаНаОбъект.Расшифровка.Выгрузить();
	тзКонтрагенты.Свернуть("Контрагент,ДоговорКонтрагента");
	
	Для Каждого СтрокаКоллекции ИЗ тзКонтрагенты Цикл
		СтруктураПоиска=Новый Структура;
		СтруктураПоиска.Вставить("Контрагент",СтрокаКоллекции.Контрагент);
		СтруктураПоиска.Вставить("ДоговорКонтрагента",СтрокаКоллекции.ДоговорКонтрагента);
		МассивСтрок=тзИсходная.НайтиСтроки(СтруктураПоиска);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Контрагент=СтрокаКоллекции.Контрагент;
		ОбластьМакета.Параметры.ДоговорКонтрагента=СтрокаКоллекции.ДоговорКонтрагента;
		ТабДокумент.Вывести(ОбластьМакета);
		ИтогоСуммаПринятыхРасходов=0; ИтогоСуммаНеПринятыхРасходов=0; ИтогоСуммаНеУСН=0;
		Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,СтрокаМассива,,"Контрагент,ДоговорКонтрагента");
			ТабДокумент.Вывести(ОбластьМакета);
			ИтогоСуммаПринятыхРасходов=ИтогоСуммаПринятыхРасходов+СтрокаМассива.СуммаПринятыхРасходов;
			ИтогоСуммаНеПринятыхРасходов=ИтогоСуммаНеПринятыхРасходов+СтрокаМассива.СуммаНеПринятыхРасходов;
			ИтогоСуммаНеУСН=ИтогоСуммаНеУСН+СтрокаМассива.СуммаНеПринимаетсяУСН;
		КонецЦикла;
		ОбластьМакета = Макет.ПолучитьОбласть("Итог");
		ОбластьМакета.Параметры.ИтогоСуммаПринятыхРасходов=ИтогоСуммаПринятыхРасходов;
		ОбластьМакета.Параметры.ИтогоСуммаНеПринятыхРасходов=ИтогоСуммаНеПринятыхРасходов;
		ОбластьМакета.Параметры.ИтогоСуммаНеУСН=ИтогоСуммаНеУСН;
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;	
	
	Возврат ТабДокумент;
КонецФункции	

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="Расшифровка" Тогда
		ТабДокумент=Печать_Расшифровка(СтруктураПараметров);
	КонецЕсли;	
	Возврат ТабДокумент;
КонецФункции