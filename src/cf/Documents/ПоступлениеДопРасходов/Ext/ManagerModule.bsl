////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("Накладная", "Доп. расходы");
	Структура.Вставить("Акт", "Акт об оказании услуг");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция Печать_ПоступлениеДопРасходов(СтруктураПараметров,ТабДокумент)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;

	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.УстановитьПараметр("Курс",            СсылкаНаОбъект.КурсВзаиморасчетов);
	Запрос.УстановитьПараметр("Кратность",       СсылкаНаОбъект.КратностьВзаиморасчетов);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Номер,
	|	Дата,
	|	ДоговорКонтрагента,
	|	Контрагент,
	|	Организация,
	//|	Содержание,
	|	СуммаДокумента КАК СуммаДокумента,
	|	ВалютаДокумента,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС,
//	|	СтавкаНДС,
	|	ВложенныйЗапрос.СуммаНДСТовары + СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапрос.СуммаДенег                КАК СуммаДенег,
	|	ВложенныйЗапрос.Количество                КАК Количество
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов,
	|	(ВЫБРАТЬ
	|		СУММА(ПоступлениеДопРасходовТовары.Сумма)             КАК СуммаДенег,
	|		СУММА(ПоступлениеДопРасходовТовары.СуммаНДС)          КАК СуммаНДСТовары,
	|		КОЛИЧЕСТВО(ПоступлениеДопРасходовТовары.Номенклатура) КАК Количество
	|
	|	ИЗ
	|		Документ.ПоступлениеДопРасходов.Товары КАК ПоступлениеДопРасходовТовары
	|
	|	ГДЕ
	|		Ссылка = &ТекущийДокумент) КАК ВложенныйЗапрос
	|
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка = &ТекущийДокумент
	|";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	Если ТабДокумент=Неопределено Тогда
		ТабДокумент=Новый ТабличныйДокумент;	
	КонецЕсли;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеДопРасходов_Накладная";
	
	Макет=ИнициализацияМакета(СтруктураПараметров, "Накладная");

	// Выводим шапку накладной

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(СсылкаНаОбъект, "Доп. расходы");
	ТабДокумент.Вывести(ОбластьМакета);

	ПредставлениеОрганизации = ПечатныеФормыСервер.ОписаниеОрганизации(КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата), "ПолноеНаименование,");
	ПредставлениеКонтрагента = ПечатныеФормыСервер.ОписаниеОрганизации(КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.Дата), "ПолноеНаименование,");

	ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
	ОбластьМакета.Параметры.ПредставлениеПоставщика = ПредставлениеКонтрагента;
	ОбластьМакета.Параметры.Поставщик = Шапка.Контрагент;
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
	ОбластьМакета.Параметры.ПредставлениеПолучателя = ПредставлениеОрганизации;
	ОбластьМакета.Параметры.Получатель = Шапка.Организация;
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести табличную часть
	ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	СуммаВсего = Шапка.СуммаДокумента - ?(Шапка.СуммаВключаетНДС, 0, Шапка.СуммаНДС);
	СуммаИтого = СуммаВсего;

	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	ОбластьМакета.Параметры.НомерСтроки = 1;
	//ОбластьМакета.Параметры.Товар       = Шапка.Содержание;
	ОбластьМакета.Параметры.Цена        = СуммаВсего;
	ОбластьМакета.Параметры.Количество  = 1;
	ОбластьМакета.Параметры.Сумма       = СуммаВсего;
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести Итого
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.Всего = ОбщегоНазначения.ФорматСумм(СуммаИтого);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести ИтогоНДС
	Если Шапка.УчитыватьНДС Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьМакета.Параметры.ВсегоНДС = ОбщегоНазначения.ФорматСумм(Шапка.СуммаНДС);
		ОбластьМакета.Параметры.НДС = ?(Шапка.СуммаВключаетНДС, "В том числе НДС:", "Сумма НДС:");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	// Вывести Сумму прописью
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
	СуммаКПрописи = СуммаВсего + ?(Шапка.СуммаВключаетНДС, 0, Шапка.СуммаНДС);
	ОбластьМакета.Параметры.ИтоговаяСтрока ="Всего наименований " + 1
	+ ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Шапка.ВалютаДокумента);
	ОбластьМакета.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести подписи
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

Функция Печать_АктОбОказанииУслуг(СтруктураПараметров,ТабДокумент, СуммыВРублях = Ложь)

	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;
	ЗапросШапка=Новый Запрос;
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	ЗапросШапка.Текст="
	|ВЫБРАТЬ
	|	Номер,
	|	Дата,
	|	ДоговорКонтрагента,
	|	Контрагент КАК Поставщик,
	|	Организация КАК Получатель,
	|	Комментарий,
	|	Организация,
	|	СуммаДокумента,
	|	ВалютаДокумента,
	|	КурсВзаиморасчетов      КАК Курс,
	|	КратностьВзаиморасчетов КАК Кратность,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеДопРасходов КАК ПоступлениеДопРасходов
	|
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка = &ТекущийДокумент
	|";
	Шапка = ЗапросШапка.Выполнить().Выбрать();
	Шапка.Следующий();

	СтрокаВыборкиПоляСодержания = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("ПоступлениеДопРасходов");

	ЗапросУслуги=Новый Запрос;
	ЗапросУслуги.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	ЗапросУслуги.УстановитьПараметр("Курс",            ?(СуммыВРублях, Шапка.Курс,      1));
	ЗапросУслуги.УстановитьПараметр("Кратность",       ?(СуммыВРублях, Шапка.Кратность, 1));
	ЗапросУслуги.Текст="
	|ВЫБРАТЬ
	|	Содержание КАК Номенклатура,
	|	Содержание КАК Товар,
	|	1 КАК Количество,
	|	Null КАК ЕдиницаИзмерения,
	|	Сумма     * &Курс / &Кратность КАК Цена,
	|	Сумма    * &Курс / &Кратность КАК Сумма,
	|	СтавкаНДС,
	|	СуммаНДС * &Курс / &Кратность КАК СуммаНДС,
	|	1 КАК Метка
	|ИЗ
	|	Документ.ПоступлениеДопРасходов.РаспределяемыеУслуги КАК ПоступлениеДопРасходов
	|ГДЕ
	|	ПоступлениеДопРасходов.Ссылка = &ТекущийДокумент
	|";

	ТаблицаУслуги = ЗапросУслуги.Выполнить().Выгрузить();

	Если ТабДокумент=Неопределено Тогда
		ТабДокумент=Новый ТабличныйДокумент;	
	КонецЕсли;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеДопРасходов_Акт";
	
	Макет=ИнициализацияМакета(СтруктураПараметров, "Акт");

	Руководители = ПечатныеФормыСервер.ОтветственныеЛицаОрганизации(Шапка.Организация, Шапка.Дата,СсылкаНаОбъект);
	ДолжностьРуководителя = Руководители.РуководительДолжность;
	Руководитель=УправлениеКонфигурациейСервер.ФИО(Руководители.РуководительФизЛицо, "Родительный"); 

	СведенияОрганизации=КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
	
	ПредставлениеПолучателя = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОрганизации, "ПолноеНаименование,");
	ПредставлениеПолучателя1=ПредставлениеПолучателя+" в лице "+ДолжностьРуководителя+"а "+Руководитель+" действующего на основании устава";
	
	ПредставлениеПоставщика = ПечатныеФормыСервер.ОписаниеОрганизации(КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Поставщик, Шапка.Дата), "ПолноеНаименование,");
	ПредставлениеПоставщика1=ПредставлениеПоставщика+" в лице "+Шапка.Поставщик.ПараметрыПечати_Должность+" "+Шапка.Поставщик.ПараметрыПечати_ВЛице+" действующего на основании "+Шапка.Поставщик.ПараметрыПечати_Основание;
	
	ОбластьМакета=Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.КДоговору="к договору №"+СокрЛП(Шапка.ДоговорКонтрагента.Номер)+" от "+Формат(Шапка.ДоговорКонтрагента.Дата, "ДЛФ=D");
	ОбластьМакета.Параметры.ТекстЗаголовка="Акт приемки-сдачи работ № "+Строка(Шапка.Номер)+" от "+Формат(Шапка.Дата, "ДЛФ=Д");
	ОбластьМакета.Параметры.Исполнитель=ПредставлениеПоставщика1;
	ОбластьМакета.Параметры.Заказчик=ПредставлениеПолучателя1;
	ОбластьМакета.Параметры.Комментарий="Комментарий:"+СокрЛП(Шапка.Комментарий);
	ТабДокумент.Вывести(ОбластьМакета);

	ВалютаПечати = ?(СуммыВРублях, МодульВалютногоУчета.ПолучитьВалюту("Бух"), Шапка.ВалютаДокумента);

	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьСтроки=Макет.ПолучитьОбласть("Строка"); НомерСтроки = 0;
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаУслуги Цикл	
		НомерСтроки = НомерСтроки + 1;

		ОбластьСтроки.Параметры.Заполнить(СтрокаТабличнойЧасти);
		ОбластьСтроки.Параметры.НомерСтроки=НомерСтроки;
		ОбластьСтроки.Параметры.НоменклатураНаименование=СокрЛП(СтрокаТабличнойЧасти.Товар);
		ОбластьСтроки.Параметры.СтоимостьСНДС=СтрокаТабличнойЧасти.Сумма+?(Шапка.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
		ТабДокумент.Вывести(ОбластьСтроки);
	КонецЦикла;

	Сумма=0; СуммаНДС=0; ВсегоНаСумму=0;
	Если ТаблицаУслуги <> Неопределено Тогда
		Сумма    = ТаблицаУслуги.Итог("Сумма");
		СуммаНДС = ТаблицаУслуги.Итог("СуммаНДС");
		ВсегоНаСумму = Сумма+?(Шапка.СуммаВключаетНДС, 0, СуммаНДС);
	КонецЕсли;

	ОбластьМакета=Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.СтоимостьСНДС=ОбщегоНазначения.ФорматСумм(ВсегоНаСумму);
	ТабДокумент.Вывести(ОбластьМакета);

	Если Шапка.УчитыватьНДС Тогда
		ОбластьМакета=Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьМакета.Параметры.СуммаНДС=ОбщегоНазначения.ФорматСумм(СуммаНДС);
		ОбластьМакета.Параметры.НДС=?(Шапка.СуммаВключаетНДС, "В том числе НДС", " Сумма НДС");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	ОбластьМакета=Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьМакета.Параметры.СуммаПрописью=ОбщегоНазначения.СформироватьСуммуПрописью(ВсегоНаСумму, ВалютаПечати);
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета=Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.ДатаПодписи=Формат(Шапка.Дата, "ДЛФ=DD");
	ОбластьМакета.Параметры.ФактическийАдрес=ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОрганизации, "ФактическийАдрес,");
	ОбластьМакета.Параметры.Отпустил=ПредставлениеПоставщика;
	ОбластьМакета.Параметры.Получил=ПредставлениеПолучателя;
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	//ТабДокумент=Неопределено;
	//ИмяМакета=СтруктураПараметров.ИмяМакета;
	//СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;

	Если СтруктураПараметров.ИмяМакета = "Накладная" тогда
		ТабДокумент = Печать_ПоступлениеДопРасходов(СтруктураПараметров,ТабДокумент);		
	ИначеЕсли СтруктураПараметров.ИмяМакета = "Акт" Тогда
		//ТабДокумент = Печать_АктОбОказанииУслуг(СтруктураПараметров);
	    ТабДокумент=Печать_АктОбОказанииУслуг(СтруктураПараметров,ТабДокумент);
	КонецЕсли;

	Возврат ТабДокумент;
КонецФункции
