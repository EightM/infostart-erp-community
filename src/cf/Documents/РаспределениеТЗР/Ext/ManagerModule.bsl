Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("РаспределениеТЗР", "Распределение транспортно-заготовительных расходов");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция ПолучитьРеквизитыШапки(СсылкаНаОбъект, стрРеквизиты="")
	Если НЕ ПустаяСтрока(стрРеквизиты) Тогда стрРеквизиты=","+стрРеквизиты; КонецЕсли; 
	Для каждого мдРеквизит Из СсылкаНаОбъект.Метаданные().Реквизиты Цикл
		стрРеквизиты=стрРеквизиты+","+Символы.ПС+мдРеквизит.Имя;
	КонецЦикла;	

	Запрос=Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Дата,
	|	Номер,
	|	Проведен
	|	"+стрРеквизиты+"
	|ИЗ
	|	Документ.РаспределениеТЗР КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка = &ТекущийДокумент
	|";
	Запрос.Выполнить();

	РеквизитыШапки=Запрос.Результат.Выбрать();
	РеквизитыШапки.Следующий();

	Возврат РеквизитыШапки;
КонецФункции 	

Функция ПолучитьРеквизитыТабличнойЧасти_Товары(СсылкаНаОбъект)
	Запрос=Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	Документ.РаспределениеТЗР.Товары КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка = &ТекущийДокумент
	|";
	Запрос.Выполнить();
	Возврат Запрос.Результат.Выгрузить();	
КонецФункции

Функция Печать_ТЗР(СтруктураПараметров, ИмяМакета)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;

	РеквизитыШапки=ПолучитьРеквизитыШапки(СсылкаНаОбъект);
	тзТовары=ПолучитьРеквизитыТабличнойЧасти_Товары(СсылкаНаОбъект);
	
	ТабДокумент=Новый ТабличныйДокумент;
	//ТабДокумент.ИмяПараметровПечати="ПАРАМЕТРЫ_ПЕЧАТИ_РаспределениеТЗР_РаспределениеТЗР";
	
	Макет=ИнициализацияМакета(СтруктураПараметров, ИмяМакета);

	ОбластьМакета=Макет.ПолучитьОбласть("Заголовок");
	//БухгалтерскаяСправка
	ОбластьМакета.Параметры.ТекстЗаголовка=ОбщегоНазначения.СформироватьЗаголовокДокумента(СсылкаНаОбъект, "Распределение транспортно-заготовительных расходов ");
	ОбластьМакета.Параметры.Организация="Организация: "+РеквизитыШапки.Организация;
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета=Макет.ПолучитьОбласть("Строка");
	Для каждого СтрокаТабличнойЧасти Из тзТовары Цикл
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтрокаТабличнойЧасти);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета=Макет.ПолучитьОбласть("Подвал");
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
    Если СтруктураПараметров.ИмяМакета="РаспределениеТЗР" Тогда
		ТабДокумент=Печать_ТЗР(СтруктураПараметров, СтруктураПараметров.ИмяМакета);
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции
