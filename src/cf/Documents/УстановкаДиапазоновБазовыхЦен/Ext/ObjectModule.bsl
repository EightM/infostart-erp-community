Процедура ПроверитьЗаполнениеРеквизитовДокумента(ТаблицаДиапазонов, СтруктураШД, Отказ, Заголовок)
Перем ПредыдущаяСтрокаТЗ;

	Если ТаблицаДиапазонов.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнена шкала диапазонов.", Отказ, Заголовок);
	КонецЕсли;

	ТаблицаДиапазонов.Сортировать("ВерхняяГраница");

	КоличествоСтрок = ТаблицаДиапазонов.Количество() - 1;
	Для Тмп = 0 По КоличествоСтрок Цикл
		ТекущаяСтрокаТЗ = ТаблицаДиапазонов.Получить(Тмп);
		СтрокаНачалаСообщенияОбОшибке = "В строке номер " + СокрЛП(ТекущаяСтрокаТЗ.НомерСтроки);
		Если Тмп > 0 Тогда
			Если ТекущаяСтрокаТЗ.ВерхняяГраница = ПредыдущаяСтрокаТЗ.ВерхняяГраница Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " продублирована верхняя граница.", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
		ПредыдущаяСтрокаТЗ = ТекущаяСтрокаТЗ;
	КонецЦикла;

КонецПроцедуры

Процедура ДвиженияПоРегистрам(СтруктураШД, ТаблицаПоДиапазонам, Отказ, Заголовок)
    Если Отказ Тогда Возврат; КонецЕсли; 

	НаборДвижений = Движения.ДиапазоныЦенДляНаценки;
	ТаблицаДвижений = НаборДвижений.ВыгрузитьКолонки();

	// Заполним таблицу движений.
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаПоДиапазонам, ТаблицаДвижений);

	// ...и заполним колонку "ТипЦен".
	ТаблицаДвижений.ЗаполнитьЗначения(СтруктураШД.ТипЦен, "ТипЦен");
	
	НаборДвижений.мПериод = СтруктураШД.Дата;
	НаборДвижений.мТаблицаДвижений = ТаблицаДвижений;

	Если Не Отказ Тогда
		Движения.ДиапазоныЦенДляНаценки.ВыполнитьДвижения();
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УправлениеДокументамиСервер.ПередПроведением(Отказ, РежимПроведения, ЭтотОбъект);
	Если Отказ Тогда Возврат; КонецЕсли; 
	
	Заголовок=ДополнительныеСвойства.Заголовок;
	СтруктураШД = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);
	ДеревоПолейЗапросаПоШапке = УправлениеЗапасами.СформироватьДеревоПолейЗапросаПоШапке();
	СтруктураШД = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШД, "");
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("НомерСтроки",                "НомерСтроки");
	СтруктураПолей.Вставить("ВерхняяГраница",             "ВерхняяГраница");
	СтруктураПолей.Вставить("Цена",                       "Цена");
	СтруктураПолей.Вставить("Валюта",                     "Валюта");
	РезультатЗапросаПоДиапазонам = УправлениеЗапасами.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "ШкалаДиапазонов", СтруктураПолей);
	ТаблицаПоДиапазонам = РезультатЗапросаПоДиапазонам.Выгрузить();
	ПроверитьЗаполнениеРеквизитовДокумента(ТаблицаПоДиапазонам, СтруктураШД, Отказ, Заголовок);

	ДвиженияПоРегистрам(СтруктураШД, ТаблицаПоДиапазонам, Отказ, Заголовок);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Операторы основной программы

УправлениеДокументамиСервер.ИнициализацияМодуля(ДополнительныеСвойства);

