////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("РасчетРеализованногоНаложения", "Расчет реализованного наложения");
	Структура.Вставить("РасчетРезервовПоСомнительнымДолгамБУ", "Расчет резервов по сомнительным долгам (БУ)");
	Структура.Вставить("РасчетРезервовПоСомнительнымДолгамНУ", "Расчет резервов по сомнительным долгам (НУ)");
	Структура.Вставить("БухгалтерскаяСправка", "Бухгалтерская справка");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция РасчетРеализованногоНаложения(СтруктураПараметров)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;
	
	ТабДокумент=Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РегламентнаяОперация_РасчетРеализованногоНаложения";
	
	Макет=ИнициализацияМакета(СтруктураПараметров, "РасчетРеализованногоНаложения");
	
	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заголовок= "РАСЧЕТ РЕАЛИЗОВАННОГО НАЛОЖЕНИЯ ЗА "+Формат(СсылкаНаОбъект.Дата, "ДФ='MMMM yyyy'");
	ТабДокумент.Вывести(ОбластьМакета);
	
	тзДанные=СсылкаНаОбъект.РасчетРеализованногоНаложения.Выгрузить();
	тзСклады=СсылкаНаОбъект.РасчетРеализованногоНаложения.Выгрузить();
	тзСклады.Свернуть("Склад","СальдоНаНачалоПериода,ОборотПоДебету,ОборотПоКредиту,СальдоПредварительное,ТовароОборот,ОстатокТоваровНаКонецПериода,Итого,СуммаНаценкиНаОстаткеТовара,РеализованноеНаложение");
	Для КАждого СтрокаТз ИЗ тзСклады Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСклад");
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,СтрокаТз);
		ТабДокумент.Вывести(ОбластьМакета);
		МассивСтрок=тзДанные.НайтиСтроки(Новый Структура("Склад",СтрокаТз.Склад));
		Для Каждого СтрокаМ ИЗ МассивСтрок Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТовар");
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,СтрокаМ);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;	
	КонецЦикла;	
	
	тзСклады.Свернуть("","СальдоНаНачалоПериода,ОборотПоДебету,ОборотПоКредиту,СальдоПредварительное,ТовароОборот,ОстатокТоваровНаКонецПериода,Итого,СуммаНаценкиНаОстаткеТовара,РеализованноеНаложение");
	ОбластьМакета=Макет.ПолучитьОбласть("Подвал");
	ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры,тзСклады[0]);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;	
КонецФункции

Функция РасчетРезервовПоСомнительнымДолгам(СтруктураПараметров,ВариантОтчета)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;
	Макет      = ПолучитьМакет("РасчетРезервовПоСомнительнымДолгам");
	ДокументРезультат=Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаОбъект",СсылкаНаОбъект);
	Запрос.УстановитьПараметр("Организация",СсылкаНаОбъект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала",НачалоГода(СсылкаНаОбъект.Дата));
	Запрос.УстановитьПараметр("ДатаКонца",КонецМесяца(СсылкаНаОбъект.Дата));
	Запрос.УстановитьПараметр("ВидОперации",Перечисления.ВидыРегламентныхОпераций.РасчетРезервовПоСомнительнымДолгам);
	
	Если ВариантОтчета = 2 Тогда
		Запрос.Текст	= 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(РасчетРезервовПоСомнительнымДолгам.НачисленоСНачалаГодаНУ),0) КАК НачисленоНУСНачалаГода
		|ИЗ
		|	Документ.РегламентнаяОперация.РасчетРезервовПоСомнительнымДолгам КАК РасчетРезервовПоСомнительнымДолгам
		|ГДЕ
		|	РасчетРезервовПоСомнительнымДолгам.Ссылка.Организация = &Организация
		|	И РасчетРезервовПоСомнительнымДолгам.Ссылка.ВидОперации = &ВидОперации
		|	И РасчетРезервовПоСомнительнымДолгам.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
		|	И РасчетРезервовПоСомнительнымДолгам.Ссылка.Проведен = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	0 КАК ДоляРезервовПоСомнительнымДолгамНУ
		//|	ЕСТЬNULL(ДолиСписанияКосвенныхРасходов.ДоляРезервовПоСомнительнымДолгамНУ,0) КАК ДоляРезервовПоСомнительнымДолгамНУ
		//|ИЗ
		//|	РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиСписанияКосвенныхРасходов
		//|ГДЕ
		//|	ДолиСписанияКосвенныхРасходов.Организация = &ГоловнаяОрганизация
		//|	И ДолиСписанияКосвенныхРасходов.ПериодРасчета >= &ДатаНач
		//|	И ДолиСписанияКосвенныхРасходов.ПериодРасчета <= &ДатаКон
		//|	И ДолиСписанияКосвенныхРасходов.Активность
		|;";
	Иначе
		Запрос.Текст	=
		"ВЫБРАТЬ
		|	0 КАК НачисленоНУ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	0 КАК ДоляРезервовПоСомнительнымДолгамНУ
		|;";
	КонецЕсли;
	
	Запрос.Текст	= Запрос.Текст +
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетРезервовПоСомнительнымДолгам.Ссылка.Дата КАК Период,
	|	РасчетРезервовПоСомнительнымДолгам.Контрагент КАК Контрагент,
	|	РасчетРезервовПоСомнительнымДолгам.Контрагент.Наименование КАК КонтрагентНаименование,
	|	РасчетРезервовПоСомнительнымДолгам.ДоговорКонтрагента КАК Договор,
	|	РасчетРезервовПоСомнительнымДолгам.ДоговорКонтрагента.Наименование КАК ДоговорНаименование,
	|	РасчетРезервовПоСомнительнымДолгам.ДокументРасчетовСКонтрагентом КАК ДокументДолга,
	|	РасчетРезервовПоСомнительнымДолгам.СуммаЗадолженности КАК СуммаДолга,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ КАК НачисленоРанее,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ - РасчетРезервовПоСомнительнымДолгам.НачисленоРанееНУ КАК НачисленоРанееПР,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоБУ КАК Начислено,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоБУ - РасчетРезервовПоСомнительнымДолгам.НачисленоНУ КАК НачисленоПР,
	|	РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ КАК Восстановлено,
	|	РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ - РасчетРезервовПоСомнительнымДолгам.ВосстановленоНУ КАК ВосстановленоПР,
	|	РасчетРезервовПоСомнительнымДолгам.ПрисоединеноБУ КАК Присоединено,
	|	РасчетРезервовПоСомнительнымДолгам.ПрисоединеноБУ КАК ПрисоединеноПР,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоСНачалаГодаНУ КАК НачисленоСНачалаГодаНУ,
	|	РасчетРезервовПоСомнительнымДолгам.ДатаВозникновенияЗадолженности КАК ДатаВозникновенияЗадолженности,
	|	РасчетРезервовПоСомнительнымДолгам.СрокЗадолженности КАК СрокЗадолженности,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ + РасчетРезервовПоСомнительнымДолгам.НачисленоБУ - РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ КАК Остаток,
	|	РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ + РасчетРезервовПоСомнительнымДолгам.НачисленоБУ - РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ - (РасчетРезервовПоСомнительнымДолгам.НачисленоРанееНУ + РасчетРезервовПоСомнительнымДолгам.НачисленоНУ - РасчетРезервовПоСомнительнымДолгам.ВосстановленоНУ) КАК ОстатокПР,
	|	РасчетРезервовПоСомнительнымДолгам.СуммаЗадолженности - РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ - РасчетРезервовПоСомнительнымДолгам.НачисленоБУ + РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ КАК Остаток2,
	|	РасчетРезервовПоСомнительнымДолгам.СуммаЗадолженности - РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ - РасчетРезервовПоСомнительнымДолгам.НачисленоБУ + РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ - (РасчетРезервовПоСомнительнымДолгам.СуммаЗадолженности - РасчетРезервовПоСомнительнымДолгам.НачисленоРанееНУ - РасчетРезервовПоСомнительнымДолгам.НачисленоНУ + РасчетРезервовПоСомнительнымДолгам.ВосстановленоНУ) КАК Остаток2ПР,
	|	ВЫБОР
	|		КОГДА РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ = 0
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРезервовПоСомнительнымДолгам.ВосстановленоБУ / РасчетРезервовПоСомнительнымДолгам.НачисленоРанееБУ
	|	КОНЕЦ КАК ДоляОплаты
	|ИЗ
	|	Документ.РегламентнаяОперация.РасчетРезервовПоСомнительнымДолгам КАК РасчетРезервовПоСомнительнымДолгам
	|ГДЕ
	|	РасчетРезервовПоСомнительнымДолгам.Ссылка.Организация = &Организация
	|	И РасчетРезервовПоСомнительнымДолгам.Ссылка = &СсылкаНаОбъект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	КонтрагентНаименование,
	|	ДоговорНаименование,
	|	ДокументДолга
	|ИТОГИ
	|	СУММА(СуммаДолга),
	|	СУММА(НачисленоРанее),
	|	СУММА(НачисленоРанееПР),
	|	СУММА(Начислено),
	|	СУММА(НачисленоПР),
	|	СУММА(Восстановлено),
	|	СУММА(ВосстановленоПР),
	|	СУММА(Присоединено),
	|	СУММА(ПрисоединеноПР),
	|	СУММА(НачисленоСНачалаГодаНУ),
	|	МИНИМУМ(ДатаВозникновенияЗадолженности),
	|	МИНИМУМ(СрокЗадолженности),
	|	СУММА(Остаток),
	|	СУММА(ОстатокПР),
	|	СУММА(Остаток2),
	|	СУММА(Остаток2ПР),
	|	МИНИМУМ(ДоляОплаты)
	|ПО
	|	Период,
	|	Контрагент,
	|	Договор,
	|	ДокументДолга";
	
	Если ВариантОтчета = 2 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".НачисленоРанееБУ",  ".НачисленоРанееНУ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".НачисленоБУ",  ".НачисленоНУ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".ВосстановленоБУ",  ".ВосстановленоНУ");
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если ВариантОтчета = 2 Тогда
		СтруктураПараметров = Новый Структура;		
		СписокОП = ОбщегоНазначения.ПолучитьСписокОбособленныхПодразделенийОрганизации(СсылкаНаОбъект.Организация);
		СписокОП.Добавить(СсылкаНаОбъект.Организация.Ссылка, СсылкаНаОбъект.Организация.Наименование);
		СтруктураПараметров.Вставить("СписокОрганизаций", СписокОП);
		СтруктураПараметров.Вставить("Организация",       СсылкаНаОбъект.Организация);
		СтруктураПараметров.Вставить("Дата",              КонецМесяца(СсылкаНаОбъект.Дата));
		ДокОбъект=Документы.РегламентнаяОперация.СоздатьДокумент();
		СуммаВыручки = ОКР(ДокОбъект.ОпределитьНормуРасходовПоВыручке(СтруктураПараметров, 10),0);
	КонецЕсли;
	
	ОбластьШапка    = Макет.ПолучитьОбласть("Шапка" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьСтрока1  = Макет.ПолучитьОбласть("Строка1" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьСтрока2  = Макет.ПолучитьОбласть("Строка2" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьСтрока3  = Макет.ПолучитьОбласть("Строка3" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьСтрокаПР = Макет.ПолучитьОбласть("СтрокаПР");
	
	ОбластьИтого            = Макет.ПолучитьОбласть("Итого" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьИтогоПР          = Макет.ПолучитьОбласть("ИтогоПР");
	ОбластьОкончаниеТаблицы = Макет.ПолучитьОбласть("ОкончаниеТаблицы" + ?(ВариантОтчета = 2, "НУ", ""));
	ОбластьПодвал           = Макет.ПолучитьОбласть("Подвал");
	ЗаголовокОтчета=Макет.ПолучитьОбласть("Заголовок");
	
	Если ВариантОтчета = 2 Тогда
		ВыборкаНачисленоСНачалаГода = Результат[0].Выбрать();
		ВыборкаНачисленоСНачалаГода.Следующий();
		НачисленоСНачалаГода = ОКР(ВыборкаНачисленоСНачалаГода.НачисленоНУСНачалаГода,0);
		ВыборкаДоля = Результат[1].Выбрать();
		ВыборкаДоля.Следующий();
		ДоляРезервовПоСомнительнымДолгамНУ = ВыборкаДоля.ДоляРезервовПоСомнительнымДолгамНУ;
	КонецЕсли;
	
	ВыборкаПериод = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПериод.Следующий() Цикл
		
		ЗаголовокОтчета.Параметры.Период              = "" + ПредставлениеПериода(НачалоМесяца(ВыборкаПериод.Период), КонецМесяца(ВыборкаПериод.Период), "ФП = Истина");
		ЗаголовокОтчета.Параметры.НазваниеОрганизации = СсылкаНаОбъект.Организация.Наименование;
		ЗаголовокОтчета.Параметры.ДатаСоставления     = КонецМесяца(ВыборкаПериод.Период);
		ЗаголовокОтчета.Параметры.НазваниеОтчета      = "Резервы по сомнительным долгам (" +?(ВариантОтчета = 2, "налоговый учет", "бухгалтерский учет") + ")";
		ДокументРезультат.Вывести(ЗаголовокОтчета);
		Если ВариантОтчета = 2 Тогда
			ОбластьШапка.Параметры.ТекстШапки = "* " + ДоляРезервовПоСомнительнымДолгамНУ;
		КонецЕсли;
		ОбластьШапка.Параметры.ДатаКон = Формат(КонецМесяца(СсылкаНаОбъект.Дата), "ДЛФ=D");
		ДокументРезультат.Вывести(ОбластьШапка);
		
		// Параметр для показа заголовка
		ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;
		
		ВыборкаКонтрагент = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаКонтрагент.Следующий() Цикл
			
			ОбластьСтрока1.Параметры.Заполнить(ВыборкаКонтрагент);
			ДокументРезультат.Вывести(ОбластьСтрока1);
			
			ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаДоговор.Следующий() Цикл
				
				ОбластьСтрока2.Параметры.Заполнить(ВыборкаДоговор);
				ДокументРезультат.Вывести(ОбластьСтрока2);
				Выборка = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока Выборка.Следующий() Цикл
					
					ОбластьСтрока3.Параметры.Заполнить(Выборка);
					ДокументРезультат.Вывести(ОбластьСтрока3);
					
					//Если ВариантОтчета = 3 Тогда
					//	Если ПоддержкаПБУ18 Тогда
					//		ОбластьСтрокаПР.Параметры.Заполнить(Выборка);
					//	КонецЕсли;
					//	ДокументРезультат.Вывести(ОбластьСтрокаПР);
					//КонецЕсли;
					
				КонецЦикла; // документ	
			КонецЦикла;  // ДОговор
		КонецЦикла;  // Контрагент
		
		ОбластьИтого.Параметры.Заполнить(ВыборкаПериод);
		
		ДокументРезультат.Вывести(ОбластьИтого);
		//Если ВариантОтчета = 3 Тогда
		//	Если ПоддержкаПБУ18 Тогда
		//		ОбластьИтогоПР.Параметры.Заполнить(ВыборкаПериод);
		//	КонецЕсли;
		//	ДокументРезультат.Вывести(ОбластьИтогоПР);
		//КонецЕсли;

		ДокументРезультат.Вывести(ОбластьОкончаниеТаблицы);
		
		Если ВариантОтчета = 3 Тогда
			ОбластьПодвал.Параметры.ТекстПримечания = "ПР: Постоянные разницы в оценке стоимости резервов по сомнительным долгам.";
		ИначеЕсли ВариантОтчета = 2 Тогда
			ОбластьПодвал.Параметры.ТекстПримечания = "Итоговая сумма графы (9) не может превышать 10% от суммы выручки: " + СуммаВыручки;
		КонецЕсли;
		
		ДокументРезультат.Вывести(ОбластьПодвал);
		
		ВысотаПодписи = ДокументРезультат.Области.Подвал.Низ - ДокументРезультат.Области.Подвал.Верх;
	КонецЦикла; // Период	
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	Возврат ДокументРезультат;
КонецФункции	

Процедура Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="РасчетРеализованногоНаложения" Тогда
		ТабДокумент=РасчетРеализованногоНаложения(СтруктураПараметров);

	ИначеЕсли СтруктураПараметров.ИмяМакета="БухгалтерскаяСправка" Тогда
		ТабДокумент=ПечатныеФормыСервер.БухгалтерскаяСправка(СтруктураПараметров, "Хозрасчетный");

	ИначеЕсли СтруктураПараметров.ИмяМакета="РасчетРезервовПоСомнительнымДолгамБУ" Тогда
		ТабДокумент=РасчетРезервовПоСомнительнымДолгам(СтруктураПараметров,1);

	ИначеЕсли СтруктураПараметров.ИмяМакета="РасчетРезервовПоСомнительнымДолгамНУ" Тогда
		ТабДокумент=РасчетРезервовПоСомнительнымДолгам(СтруктураПараметров,2);
	КонецЕсли;
КонецПроцедуры

