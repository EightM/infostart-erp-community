////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("ЗаказНаПроизводство", "Заказ на производство");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция Печать_ЗаказНаПроизводство(СтруктураПараметров, ТабДокумент)
	Запрос=Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("ТекущийДокумент", СтруктураПараметров.СсылкаНаОбъект);
	Запрос.Текст="
	|ВЫБРАТЬ *
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка = &ТекущийДокумент
	|";
	#Если НаСервере Тогда
	Запрос.Выполнить();
	#КонецЕсли
	ДанныеДокумента=Запрос.Результат.Выбрать();
	ДанныеДокумента.Следующий();

	тзТовары=ДанныеДокумента.Товары.Выгрузить();

	Если СтруктураПараметров.Свойство("МодульПечати") Тогда
		стрТекстМодуля=СтруктураПараметров.МодульПечати.ПараметрыПечатнойФормы.ТекстМодуля;
		Если Не ПустаяСтрока(стрТекстМодуля) Тогда Выполнить(стрТекстМодуля); КонецЕсли;
	КонецЕсли;	

	Если ТабДокумент=Неопределено Тогда
		ТабДокумент=Новый ТабличныйДокумент;
	КонецЕсли;
	ТабДокумент.ИмяПараметровПечати="ПараметрыПечати_ЗаказНаПроизводство";

	Макет=ИнициализацияМакета(СтруктураПараметров, "ЗаказНаПроизводство");

	// Выводим заголовок
	ОбластьМакета=Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заголовок=ОбщегоНазначения.СформироватьЗаголовокДокумента(СтруктураПараметров.СсылкаНаОбъект, "Заказ на производство");
	ОбластьМакета.Параметры.Подразделение="Подразделение: "+СокрЛП(ДанныеДокумента.ЦФО);
	ТабДокумент.Вывести(ОбластьМакета);

	// Выводим шапку
	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);
	
	//Выводим строки (номенклатуру)
	ОбластьМакета=Макет.ПолучитьОбласть("Строка");	
	Для каждого СтрокаКоллекции Из тзТовары Цикл
		ОбластьМакета.Параметры.ПорядковыйНомер=тзТовары.Индекс(СтрокаКоллекции)+1;
		ОбластьМакета.Параметры.Артикул=СтрокаКоллекции.Номенклатура.Артикул;
		ОбластьМакета.Параметры.Наименование=СтрокаКоллекции.Номенклатура;
		ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СтрокаКоллекции);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	//Выводим подвал
	ОбластьМакета=Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.НадписьВсего="Всего наименований продукции "+СокрЛП(тзТовары.Количество());
	ТабДокумент.Вывести(ОбластьМакета);

	//Выводим подписи
	ОбластьМакета=Макет.ПолучитьОбласть("Подписи");
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

Процедура Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="ЗаказНаПроизводство" Тогда
		Печать_ЗаказНаПроизводство(СтруктураПараметров, ТабДокумент);
	КонецЕсли;
КонецПроцедуры