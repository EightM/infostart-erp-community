////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("ПКО", "Приходный кассовый ордер");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция ПолучитьРеквизитыДокумента(СсылкаНаОбъект, стрРеквизиты="")
	Если НЕ ПустаяСтрока(стрРеквизиты) Тогда стрРеквизиты=","+стрРеквизиты; КонецЕсли; 
	Для каждого мдРеквизит Из СсылкаНаОбъект.Метаданные().Реквизиты Цикл
		стрРеквизиты=стрРеквизиты+","+Символы.ПС+мдРеквизит.Имя;
	КонецЦикла;	

	Запрос=Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Дата,
	|	Номер,
	|	Проведен,
	|	Дата  КАК ДатаДокумента,
	|	Организация КАК Орган,
	|	Организация КАК Руководители,
	|	Подразделение.Представление КАК ПредставлениеПодразделения,
	|	Контрагент.Представление КАК Контрагент,
	|	СуммаДокумента КАК Сумма
	|	"+стрРеквизиты+",
	|	ИсточникДанных.РасшифровкаПлатежа.(СтавкаНДС,СуммаНДС,ТОП)
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ИсточникДанных
	|ГДЕ
	|	ИсточникДанных.Ссылка = &ТекущийДокумент
	|";
	
	#Если НаСервере Тогда
	Запрос.Выполнить();
	#КонецЕсли
	Реквизиты=Запрос.Результат.Выбрать();
	Реквизиты.Следующий();

	Возврат Реквизиты;
КонецФункции 	

Функция РубКоп(Сумма)
	Руб = Цел(Сумма); Коп = ОКР(100*(Сумма-Руб),0,1);
	Возврат ""+Руб+" руб. "+Цел(Коп/10)+(Коп-10*Цел(Коп/10))+" коп.";
КонецФункции

Функция РубКопПрописью(Сумма)
	Возврат ЧислоПрописью(Сумма, , "рубль,рубля,рублей,м,копейка,копейки,копеек,ж");
КонецФункции

Функция ОпределитьКодыСчетСубсчет(тзРасшифровкаПлатежа) 
	ТОП=тзРасшифровкаПлатежа[0].ТОП;
	КодыСчетов=Новый Структура("Дебет,Кредит");
	Если ЗначениеЗаполнено(ТОП) Тогда
		Прводка=ТОП.ПроводкиБУ[0];
		КодыСчетов.Дебет=Прводка.СчетДебет;
		КодыСчетов.Кредит=Прводка.СчетКредит;
	КонецЕсли;
	Возврат КодыСчетов;	
КонецФункции

Функция Печать_ПКО(СтруктураПараметров)
    СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;
	ВалютаБухУчета=МодульВалютногоУчета.ПолучитьВалюту("Бух");

	Шапка=ПолучитьРеквизитыДокумента(СсылкаНаОбъект);

	тзРасшифровкаПлатежа=Шапка.РасшифровкаПлатежа.Выгрузить();

	ТабДокумент=Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПриходныйКассовыйОрдер_КО1";
	
	Макет=ИнициализацияМакета(СтруктураПараметров, "КО1");

	// Выводим шапку ПКО

	СведенияОбОрганизации = КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Орган, Шапка.ДатаДокумента);

	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
	ОбластьМакета.Параметры.СуммаРубКоп=?(НЕ Шапка.Касса.ВалютаДенежныхСредств=ВалютаБухУчета, Формат(Шапка.Сумма, "ЧЦ=15; ЧДЦ=2")+" "+СокрЛП(Шапка.Касса.ВалютаДенежныхСредств), РубКоп(Шапка.Сумма));		
	ОбластьМакета.Параметры.Сумма=Формат(Шапка.Сумма, "ЧЦ=15; ЧДЦ=2")+ ?(НЕ Шапка.Касса.ВалютаДенежныхСредств=ВалютаБухУчета, " " + СокрЛП(Шапка.Касса.ВалютаДенежныхСредств), "");
	ОбластьМакета.Параметры.СуммаПрописью=?(НЕ Шапка.Касса.ВалютаДенежныхСредств=ВалютаБухУчета, ОбщегоНазначения.СформироватьСуммуПрописью(Шапка.Сумма, Шапка.Касса.ВалютаДенежныхСредств), РубКопПрописью(Шапка.Сумма));
	ОбластьМакета.Параметры.ОрганизацияПоОКПО=СведенияОбОрганизации.КодПоОКПО;
	ОбластьМакета.Параметры.ДатаДокумента=Шапка.ДатаДокумента;
	ОбластьМакета.Параметры.НомерДокумента=ОбщегоНазначенияСервер.НомерНаПечать(СсылкаНаОбъект);
		
	КодыСчетСубсчет = ОпределитьКодыСчетСубсчет(тзРасшифровкаПлатежа);
	
	ОбластьМакета.Параметры.КодДебета = КодыСчетСубсчет.Дебет;
	ОбластьМакета.Параметры.СубСчет   = КодыСчетСубсчет.Кредит;
	
	ТабНДС=тзРасшифровкаПлатежа.Скопировать();	
	ТабНДС.Свернуть("СтавкаНДС", "СуммаНДС");
	
	ТекстСуммаНДС = "";
	
	Для Каждого СтрокаНДС ИЗ ТабНДС цикл
		ТекстСуммаНДС = ТекстСуммаНДС + Символы.ПС + "НДС (" + ?(НЕ ЗначениеЗаполнено(СтрокаНДС.СтавкаНДС), "без налога", СтрокаНДС.СтавкаНДС) + ") " + Формат(СтрокаНДС.СуммаНДС, "ЧЦ=15;ЧДЦ=2;ЧРД=-;ЧН=0-00");
	КонецЦикла;
	
	ТекстСуммаНДС = Сред(ТекстСуммаНДС, 2);
		
	ОбластьМакета.Параметры.ВТомЧисле = ТекстСуммаНДС;

	Руководители = ПечатныеФормыСервер.ОтветственныеЛицаОрганизации(Шапка.Руководители, КонецДня(Шапка.ДатаДокумента), СсылкаНаОбъект);
	
	Бухгалтер    = Руководители.ГлавныйБухгалтер;
	Кассир       = Руководители.Кассир;

	ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = Бухгалтер;
	ОбластьМакета.Параметры.ФИОКассира            = Кассир;

	ОбластьМакета.Параметры.ПринятоОт  = Шапка.ПринятоОт;
	ОбластьМакета.Параметры.Основание  = Шапка.Основание;
	ОбластьМакета.Параметры.Приложение = Шапка.Приложение;

	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="ПКО" Тогда
		ТабДокумент=Печать_ПКО(СтруктураПараметров);
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции