////////////////////////////////////////////////////////////////////////////////
//Управление печатными формами

Процедура СтруктураПечатныхФорм(Структура) Экспорт
	Структура.Вставить("М2", "Доверенность (М-2)");
	Структура.Вставить("М2а", "Доверенность (М-2а)");
КонецПроцедуры

Функция ИнициализацияМакета(СтруктураПараметров, стрМакет)
	Если СтруктураПараметров.Свойство("Макет") Тогда
		Возврат СтруктураПараметров.Макет;
	КонецЕсли;
	Макет=СтруктураПараметров.МакетШаблон;
	Если Макет=Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Найти(стрМакет)=Неопределено Тогда
			Макет=ПолучитьМакет(стрМакет);
		Иначе
			Макет=ПолучитьОбщийМакет(стрМакет);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
КонецФункции

Функция Печать_Доверенности(СтруктураПараметров, НазваниеМакета)
	СсылкаНаОбъект=СтруктураПараметров.СсылкаНаОбъект;

	Запрос=Новый Запрос();
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Номер КАК НомерДокумента,
	|	Дата  КАК ДатаДокумента,
	|	Организация КАК Руководители,
	|	Организация,
	|	Должность,
	|	Подразделение,
	|	ФизЛицо,
	|	Контрагент                КАК Поставщик,
	|	БанковскийСчетОрганизации КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(НаПолучениеОт КАК СТРОКА(100)) = """" ТОГДА Контрагент.НаименованиеПолное
	|		ИНАЧЕ НаПолучениеОт
	|	КОНЕЦ КАК ПоставщикПредставление,
	|	ДатаДействия                 КАК СрокДействия,
	|	ПоДокументу                  КАК РеквизитыДокументаНаПолучение,
	|	Товары.(
	|		НомерСтроки              КАК Номер,
	|		НаименованиеТовара       КАК Ценности,
	|		НаименованиеТовара       КАК ЦенностиПредставление,
	|		ЕдиницаПоКлассификатору  КАК ЕдиницаИзмерения,
	|		ЕдиницаПоКлассификатору.Представление КАК ЕдиницаИзмеренияПредставление,
	|		Количество
	|	)
	|ИЗ
	|	Документ.Доверенность КАК Доверенность
	|ГДЕ
	|	Доверенность.Ссылка = &ТекущийДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки
	|";

	Шапка=Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	ВыборкаСтрокТовары = Шапка.Товары.Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_М2";

	Макет = ИнициализацияМакета(СтруктураПараметров, "М2");

	ФамилияИмяОтчествоФизЛица     = ПечатныеФормыСервер.ФамилияИмяОтчество(Шапка.ФизЛицо, Шапка.ДатаДокумента);
	ФамилияИмяОтчествоДоверенного = СокрЛП(ФамилияИмяОтчествоФизЛица.Фамилия)+" "+СокрЛП(ФамилияИмяОтчествоФизЛица.Имя)+" "+СокрЛП(ФамилияИмяОтчествоФизЛица.Отчество);
	ДолжностьДоверенного          = Шапка.Должность;

	Если НазваниеМакета = "М2" тогда
		НазваниеФормы = "Типовая межотраслевая форма № М-2";
	Иначе
		НазваниеФормы = "Типовая межотраслевая форма № М-2а";
	КонецЕсли;

	Руководители = ПечатныеФормыСервер.ОтветственныеЛицаОрганизации(Шапка.Руководители, Шапка.ДатаДокумента,СсылкаНаОбъект);
	Руководитель = Руководители.Руководитель;
	Бухгалтер    = Руководители.ГлавныйБухгалтер;

	ПаспортФизЛица = ПечатныеФормыСервер.ПаспортныеДанные(Шапка.ФизЛицо, Шапка.ДатаДокумента);
	
	СведенияОбОрганизации = КонтактнаяИнформацияСервер.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента,, Шапка.БанковскийСчет, Шапка.Подразделение);

	НомерДокумента=ОбщегоНазначенияСервер.НомерНаПечать(СсылкаНаОбъект);
	
	ОбластьМакета=Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.НомерДокумента                = НомерДокумента;
	ОбластьМакета.Параметры.НазваниеФормы                 = НазваниеФормы;
	ОбластьМакета.Параметры.ОрганизацияПредставление      = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыСчета                = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "НомерСчета,Банк,БИК,КоррСчет,");
	
	ОбластьМакета.Параметры.РеквизитыПотребителя          = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыПотребителяАдресФактический = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны,");
	
	ОбластьМакета.Параметры.РеквизитыПотребителя          = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.РеквизитыПотребителяАдресФактический = ПечатныеФормыСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны,");
	
	ОбластьМакета.Параметры.ОрганизацияКодПоОКПО          = СведенияОбОрганизации.КодПоОКПО;
	ОбластьМакета.Параметры.ДолжностьДоверенного          = ДолжностьДоверенного;
	ОбластьМакета.Параметры.ФамилияИмяОтчествоДоверенного = ФамилияИмяОтчествоДоверенного;
	Если ПаспортФизЛица="" Тогда 
		Сообщить("Не заполнены паспортные данные физлица!");
	Иначе	
		ОбластьМакета.Параметры.ПаспортСерия              = ПаспортФизЛица.Серия;
		ОбластьМакета.Параметры.ПаспортНомер              = ПаспортФизЛица.Номер;
		ОбластьМакета.Параметры.ПаспортВыдан              = ПаспортФизЛица.Выдан;
		ОбластьМакета.Параметры.ПаспортДатаВыдачи         = ПаспортФизЛица.ДатаВыдачи;
	КонецЕсли;
	ОбластьМакета.Параметры.ДатаДокумента 				  = Формат(Шапка.ДатаДокумента,"ДФ = ""дд ММММ гггг """"г.""");
	ОбластьМакета.Параметры.СрокДействия  				  = Формат(Шапка.СрокДействия,"ДФ = ""дд ММММ гггг """"г.""");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакета);

	Пока ВыборкаСтрокТовары.Следующий() Цикл
		ОбластьМакета=Макет.ПолучитьОбласть("Строка");
		ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьМакета.Параметры.КоличествоПрописью = ?(ВыборкаСтрокТовары.Количество = 0, "",
		                                             Строка(ВыборкаСтрокТовары.Количество) + " (" + 
		                                             ПечатныеФормыСервер.КоличествоПрописью(ВыборкаСтрокТовары.Количество) + ")");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(Шапка);

	Если ЗначениеЗаполнено(Руководитель) Тогда
		ОбластьМакета.Параметры.ФИОРуководителя       = Руководитель;
		ОбластьМакета.Параметры.Руководитель          = Руководитель;
	КонецЕсли;

	Если ЗначениеЗаполнено(Бухгалтер) Тогда
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = Бухгалтер;
		ОбластьМакета.Параметры.ГлавныйБухгалтер      = Бухгалтер;
	КонецЕсли;

	ТабДокумент.Вывести(ОбластьМакета);

	Если НазваниеМакета="М2" тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Отрез");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.НомерДокумента = НомерДокумента;
		ОбластьМакета.Параметры.ФИОДоверенного = СокрЛП(Шапка.Должность)+" "+ОбщегоНазначения.ФамилияИнициалыФизЛица(ФамилияИмяОтчествоДоверенного);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	Возврат ТабДокумент;
КонецФункции

Функция Печать(СтруктураПараметров, КоличествоЭкземпляров=1, НаПринтер=Ложь, ТабДокумент=Неопределено) Экспорт
	Если СтруктураПараметров.ИмяМакета="М2" ИЛИ СтруктураПараметров.ИмяМакета="М2а" Тогда
		ТабДокумент=Печать_Доверенности(СтруктураПараметров, СтруктураПараметров.ИмяМакета);
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции