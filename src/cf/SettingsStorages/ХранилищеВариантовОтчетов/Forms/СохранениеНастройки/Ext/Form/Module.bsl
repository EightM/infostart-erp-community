&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="ЗаписатьНастройку" Тогда
		Если Не ПроверкаЗаполнения() Тогда Возврат; КонецЕсли;
		Закрыть(Новый ВыборНастроек(ЗаписатьНастройку()));

	ИначеЕсли Команда.Имя="ОткрытьНастройку" Тогда
		ТекущиеДанные=Элементы.дсСохраненыеНастройки.ТекущиеДанные;
		Если ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;
		Если ТекущиеДанные.ЭтоГруппа Тогда Возврат; КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ТекущиеДанные);
		ЗаполнитьДоступПоВыбраннойНастройке(ТекущиеДанные.Ссылка);
	КонецЕсли;
КонецПроцедуры
	
&НаСервере
Функция ЗаписатьНастройку(СправочникСсылка=Неопределено)
	Если НЕ СправочникСсылка=Неопределено Тогда 		
		СправочникОбъект=СправочникСсылка.ПолучитьОбъект();
	Иначе
		СправочникОбъект=Справочники.ВариантыОтчетов.СоздатьЭлемент();
		СправочникОбъект.Владелец=Владелец;
		СправочникОбъект.Автор=ПараметрыСеанса.ТекущийПользователь;
		СправочникОбъект.Наименование=Наименование;
		СправочникОбъект.Родитель=Родитель;
	КонецЕсли;
	СправочникОбъект.Описание=Описание;
	СправочникОбъект.ДатаНачала=ДатаНачала;
	СправочникОбъект.ДатаКонца=ДатаКонца;
	СправочникОбъект.ДоступноВсем=ДоступноВсем;

	СправочникОбъект.Доступ.Очистить();
	Для каждого СтрокаКоллекции Из Доступ Цикл
		ЗаполнитьЗначенияСвойств(СправочникОбъект.Доступ.Добавить(), СтрокаКоллекции);
	КонецЦикла;

	СправочникОбъект.Записать();

	Возврат СправочникОбъект.Ссылка;
КонецФункции

&НаКлиенте
Процедура ОбработчикОповещения_ЗаписатьНастройку(Параметр1, Параметр2) Экспорт
	Если Параметр1=КодВозвратаДиалога.Да Тогда
		СправочникСсылка=ЗаписатьНастройку(Параметр2.СправочникСсылка);
		Закрыть(Новый ВыборНастроек(СправочникСсылка));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПроверкаЗаполнения()
	Если ПустаяСтрока(Наименование) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено наименование'"),, "Наименование");
		Возврат Ложь;
	КонецЕсли;	
	Возврат Истина;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "Сохраненные настройки"

&НаКлиенте
Процедура дсСохраненыеНастройки_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	Если Элемент.ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;
	Если Элемент.ТекущиеДанные.ЭтоГруппа Тогда СтандартнаяОбработка=Истина; Возврат; КонецЕсли;

	Если НастройкиПользователяСервер.ТекущийПользователь()=Элемент.ТекущиеДанные.Автор Тогда
		ОписаниеОповещения=Новый ОписаниеОповещения("ОбработчикОповещения_ЗаписатьНастройку", ЭтотОбъект, Новый Структура("СправочникСсылка", Элемент.ТекущиеДанные.Ссылка));
		ПоказатьВопрос(ОписаниеОповещения, "Переписать настройку <"+СокрЛП(Элемент.ТекущиеДанные.Ссылка)+"> ?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Переписывать можно только свою настройку'"));
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура дсСохраненыеНастройки_ПередУдалением(Элемент, Отказ)
	Если Элемент.ТекущиеДанные.ЭтоГруппа Тогда Отказ=Истина; Возврат; КонецЕсли;
	Отказ=НЕ НастройкиПользователяСервер.ТекущийПользователь()=Элемент.ТекущиеДанные.Автор;
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Удалить настройку может только автор'"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступПоВыбраннойНастройке(ВыбраннаяНастройка)
	Доступ.Очистить();
	Для каждого СтрокаКоллекции Из ВыбраннаяНастройка.Доступ Цикл
		ЗаполнитьЗначенияСвойств(Доступ.Добавить(), СтрокаКоллекции);
	КонецЦикла; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Владелец=УправлениеКонфигурациейСервер.ИдентификаторОбъектовМетаданныхСсылка(Параметры.КлючОбъекта);
	Если Владелец.Пустая() Тогда
		УстановитьПривилегированныйРежим(Истина);
		СправочникОбъект=Справочники.ИдентификаторыОбъектовМетаданных.СоздатьЭлемент();
		СправочникОбъект.ПолноеИмя=Параметры.КлючОбъекта;
		СправочникОбъект.Наименование=Метаданные.Отчеты[стрЗаменить(Параметры.КлючОбъекта, "Отчет.", "")].Представление();
		СправочникОбъект.Записать();
		Владелец=СправочникОбъект.Ссылка;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(дсСохраненыеНастройки, "Владелец", Владелец);

	ГруппаЭлементовОтбора=ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(дсСохраненыеНастройки.Отбор.Элементы, "Автор или разрешенный пользователь", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораКоллекции(ГруппаЭлементовОтбора.Элементы, "Автор", ПараметрыСеанса.ТекущийПользователь);
	ОтборыСписковКлиентСервер.УстановитьЭлементОтбораКоллекции(ГруппаЭлементовОтбора.Элементы, "Доступ.Пользователь", ПараметрыСеанса.ТекущийПользователь);		
КонецПроцедуры