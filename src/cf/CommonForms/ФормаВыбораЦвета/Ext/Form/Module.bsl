&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="Выбрать" Тогда
		ЭтаФорма.Закрыть(Цвет);
		
	ИначеЕсли Команда.Имя="СгенерироватьСлучайныйЦвет" Тогда
		ОбработкаЦвета(0);

	ИначеЕсли Команда.Имя="СгенерироватьСлучайныйСветлыйЦвет" Тогда
		ОбработкаЦвета(1);

	ИначеЕсли Команда.Имя="СгенерироватьСлучайныйТемныйЦвет" Тогда
		ОбработкаЦвета(2);
		
	ИначеЕсли Команда.Имя="СгенерироватьСлучайныйЦветКаталогаRAL" Тогда
		ОбработкаЦвета(3);

	ИначеЕсли Команда.Имя="СгенерироватьСлучайныйWebЦвет" Тогда
		ОбработкаЦвета(4);

	ИначеЕсли Команда.Имя="ОсветлитьЦветНа10Процентов" Тогда
		ОбработкаЦвета(5);
		
	ИначеЕсли Команда.Имя="ЗатемнитьЦветНа10Процентов" Тогда
		ОбработкаЦвета(6);
		
	ИначеЕсли Команда.Имя="ГрадацияСерого" Тогда
		ОбработкаЦвета(8);		
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаЦвета(Знач Метод=0)
	Если Метод = 0 Тогда
		Цвет = УправлениеЦветом.СлучайныйЦвет();
	ИначеЕсли Метод = 1 Тогда
		Цвет = УправлениеЦветом.СлучайныйСветлыйЦвет();
	ИначеЕсли Метод = 2 Тогда
		Цвет = УправлениеЦветом.СлучайныйТемныйЦвет();
	ИначеЕсли Метод = 3 Тогда
		Цвет = УправлениеЦветом.СлучайныйЦветRALStandart();
	ИначеЕсли Метод = 4 Тогда
		Цвет = УправлениеЦветом.СлучайныйWebЦвет();
	ИначеЕсли Метод = 5 Тогда
		Цвет = УправлениеЦветом.ОсветлитьЦвет(Цвет, 10);
	ИначеЕсли Метод = 6 Тогда
		Цвет = УправлениеЦветом.ЗатемнитьЦвет(Цвет, 10);
	ИначеЕсли Метод = 7 Тогда
		Цвет = УправлениеЦветом.СмешатьДваЦвета(Цвет1, Цвет2, Баланс);
	ИначеЕсли Метод = 8 Тогда
		Цвет = УправлениеЦветом.ГрадацииСерогоAverage(Цвет);
	ИначеЕсли Метод = 9 Тогда
		Массив=УправлениеЦветом.ПолучитьГрадиент(ЦветНачалоГрадиента, ЦветКонецГрадиента, КоличествоГрадаций);
		тзГрадиент.Очистить();
		Для Каждого ЦветГрадации Из Массив Цикл
			НоваяСтрока=тзГрадиент.Добавить();
			НоваяСтрока.Название=УправлениеЦветом.ЦветВHex(ЦветГрадации);
			НоваяСтрока.Цвет=ЦветГрадации;
		КонецЦикла;
		УстановитьУсловноеОформление();
	КонецЕсли;
	ОбновитьПример();	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	Если Элементы.Страницы.ТекущаяСтраница=Элементы.СтраницаКаталогЦветов Тогда
		тзСсылка=тзЦвета; стрТаблица="тзЦвета";
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница=Элементы.СтраницаСмешиваниеЦветовГрадиент Тогда
		тзСсылка=тзГрадиент; стрТаблица="тзГрадиент";
	КонецЕсли; 
	Для каждого СтрокаКоллекции Из тзСсылка Цикл
		ЭлементОформления=УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Использование=Истина;
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", СтрокаКоллекции.Цвет);
		
		ЭлементОтбора=ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение=Новый ПолеКомпоновкиДанных(стрТаблица+".Название");
		ЭлементОтбора.ВидСравнения=ВидСравненияКомпоновкиДанных.Содержит;
		ЭлементОтбора.ПравоеЗначение=СокрЛП(СтрокаКоллекции.Название);
		
		Поле=ЭлементОформления.Поля.Элементы.Добавить();
		Поле.Использование=Истина;
		Поле.Поле=Новый ПолеКомпоновкиДанных(стрТаблица+"Отображение");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Конвертировать()
	ЦветHex=УправлениеЦветом.ЦветВHex(Цвет);
	
	Структура=УправлениеЦветом.ЦветВCMYK(Цвет);
	ЦветCMYK=СтрШаблон(НСтр("ru = 'с = %1, m = %2, y = %3, k = %4'"), Структура.c, Структура.m, Структура.y, Структура.k);
		
	Структура=УправлениеЦветом.ЦветВHSV(Цвет);
	ЦветHSV=СтрШаблон(НСтр("ru = 'h = %1, s = %2, v = %3'"), Структура.h, Структура.s, Структура.v);
		
	Структура=УправлениеЦветом.ЦветВHSL(Цвет);
	ЦветHSL=СтрШаблон(НСтр("ru = 'h = %1, s = %2, l = %3'"), Структура.h, Структура.s, Структура.l);
		
	Структура=УправлениеЦветом.АбсолютныйЦвет(Цвет);
	ЦветRGB=СтрШаблон(НСтр("ru = 'r = %1, g = %2, b = %3'"), Структура.Красный, Структура.Зеленый, Структура.Синий);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПример()
	Конвертировать();
	Элементы.Пример.ЦветФона=Цвет;
	Элементы.Пример.ЦветТекста=УправлениеЦветом.КонтрастныйЦвет(Цвет);
	Элементы.ПримерИнвертированный.ЦветФона=Элементы.Пример.ЦветТекста;
	Элементы.ПримерИнвертированный.ЦветТекста=Элементы.Пример.ЦветФона;	
КонецПроцедуры

&НаКлиенте
Процедура Страницы_ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	УстановитьУсловноеОформление();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов формы

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="ВариантТаблицыЦветов" Тогда
		Если ВариантТаблицыЦветов="Web" Тогда
			Соответствие=УправлениеЦветом.КаталогWebЦветов();
		ИначеЕсли ВариантТаблицыЦветов="CSS" Тогда
			Соответствие=УправлениеЦветом.КаталогCSSЦветов();
		ИначеЕсли ВариантТаблицыЦветов="RALStandart" Тогда
			Соответствие=УправлениеЦветом.КаталогRALStandart();
		КонецЕсли;
		тзЦвета.Очистить();
		Для Каждого СтрокаКоллекции Из Соответствие Цикл
			НоваяСтрока=тзЦвета.Добавить();
			НоваяСтрока.Название=СтрокаКоллекции.Ключ;
			НоваяСтрока.Цвет=СтрокаКоллекции.Значение;
		КонецЦикла;
		тзЦвета.Сортировать("Название");
		УстановитьУсловноеОформление();

	ИначеЕсли Элемент.Имя="Цвет" Тогда
		ОбновитьПример();
		
	ИначеЕсли Элемент.Имя="Цвет1" ИЛИ Элемент.Имя="Цвет2" ИЛИ Элемент.Имя="Баланс" Тогда
		ОбработкаЦвета(7);
		
	ИначеЕсли Элемент.Имя="ЦветНачалоГрадиента" ИЛИ Элемент.Имя="ЦветКонецГрадиента" ИЛИ Элемент.Имя="КоличествоГрадаций" Тогда
		ОбработкаЦвета(9);		
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_Регулирование(Элемент, Направление, СтандартнаяОбработка)
	Если Элемент.Имя="Цвет" Тогда
		ОбработкаЦвета(?(Направление=1, 5, 6));
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "Каталог цветов"

&НаКлиенте
Процедура тзЦвета_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.тзЦвета.ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;
	Цвет=Элементы.тзЦвета.ТекущиеДанные.Цвет; ОбновитьПример();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "Градиент"

&НаКлиенте
Процедура тзГрадиент_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.тзГрадиент.ТекущиеДанные=Неопределено Тогда Возврат; КонецЕсли;
	Цвет=Элементы.тзГрадиент.ТекущиеДанные.Цвет; ОбновитьПример();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Цвет") Тогда
		Цвет=Параметры.Цвет; ЦветУстановленПриОткрытии=Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ЦветУстановленПриОткрытии Тогда
		ОбработкаЦвета(0);
	КонецЕсли;

	Баланс=50;
	Цвет1=УправлениеЦветом.СлучайныйТемныйЦвет();
	Цвет2=УправлениеЦветом.СлучайныйСветлыйЦвет();
	
	КоличествоГрадаций=5;
	ЦветНачалоГрадиента=УправлениеЦветом.СлучайныйСветлыйЦвет();
	ЦветКонецГрадиента=УправлениеЦветом.СлучайныйТемныйЦвет();
	ОбработкаЦвета(9);

	ВариантТаблицыЦветов="Web";
	Атрибут_ПриИзменении(Элементы.ВариантТаблицыЦветов);
КонецПроцедуры

