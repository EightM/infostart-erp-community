// Функция преобразовывает таблицу значений в хранилище значения
//
// Параметры
//  Шаблоны – таблица значений
//
// Возвращаемое значение:
//   Хранилище значений
//
&НаСервере
Функция ПолучитьШаблоныНомеров()
	ТаблицаСоответствий=Новый Соответствие;
	Для каждого СтрокаШаблонов Из ШаблоныТелефонов Цикл
		Если ПустаяСтрока(СтрокаШаблонов.ШаблоныТелефонныхНомеров) Тогда Продолжить; КонецЕсли;
		ТаблицаСоответствий.Вставить(СтрЧислоВхождений(СокрЛП(СтрокаШаблонов.ШаблоныТелефонныхНомеров),"9"),СокрЛП(СтрокаШаблонов.ШаблоныТелефонныхНомеров));
	КонецЦикла;
	Возврат Новый ХранилищеЗначения(ТаблицаСоответствий);
КонецФункции

// Процедура преобразовывает хранилище значений в таблицу значений 
//
// Параметры
//  Шаблоны – хранилище значений
//
&НаСервере
Процедура ПолучитьТаблицуШаблоновТелефонов(Шаблоны)
	Если ТипЗнч(Шаблоны) <> Тип("ХранилищеЗначения") Тогда Возврат; КонецЕсли;
	СтруктураШаблонов=Шаблоны.Получить();

	ШаблоныТелефонов.Очистить();
	Если ТипЗнч(СтруктураШаблонов) = Тип("Соответствие") Тогда
		Для Индекс = 1 По 50 Цикл //думаю, больше 50-ти значных номеров не может быть
			НайденныйШаблон = СтруктураШаблонов.Получить(Индекс);
			Если НайденныйШаблон <> Неопределено Тогда
				СтрокаШаблонов = ШаблоныТелефонов.Добавить();
				СтрокаШаблонов.ШаблоныТелефонныхНомеров = НайденныйШаблон;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПроверкаДоступностиВалютыУчета()
	Возврат; //** 2020 (временно)
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Константы.ВалютаРегламентированногоУчета)
		 ИЛИ НЕ ПравоДоступа("Изменение", Метаданные.Константы.ВалютаУправленческогоУчета)
		 Тогда
		// Если нет прав на изменение хотя бы одной константы - закрываем доступ на все
		Элементы.ВалютаРегламентированногоУчета.ТолькоПросмотр=Истина;
		Элементы.ВалютаУправленческогоУчета.ТолькоПросмотр=Истина;
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;

	Для каждого Док из Метаданные.Документы Цикл
		Если Док.Имя = "УстановкаСоответствияСчетовБУиНУ" или Док.Имя = "УстановкаСоответствияСчетовБУиМСФО" Тогда
			Продолжить;
		КонецЕсли;

		Запрос.Текст = Запрос.Текст + ?(Запрос.Текст = "","", "ОБЪЕДИНИТЬ ВСЕ") + "
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Документ" + Док.Имя + ".Ссылка
		|ИЗ
		|	Документ." + Док.Имя + " КАК " + "Документ" + Док.Имя + "
		|";
	КонецЦикла;

	Попытка
		РезультатЗапроса = Запрос.Выполнить();
		ЕстьДокументы = НЕ РезультатЗапроса.Пустой();
	Исключение
		ЕстьДокументы = Истина;
	КонецПопытки;

	Элементы.ВалютаРегламентированногоУчета.ТолькоПросмотр = ЕстьДокументы;
	Элементы.ВалютаУправленческогоУчета.ТолькоПросмотр     = ЕстьДокументы;

	Если ЕстьДокументы Тогда
		Элементы.ТекстВажнойНадписи.Заголовок = "После ввода документов валюты учета изменять нельзя.";
	Иначе
		Элементы.ТекстВажнойНадписи.Заголовок = "Валюты учета можно изменять пока не введены документы.";
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПолучитьТаблицуШаблоновТелефонов(Константы.ШаблоныТелефонныхНомеров.Получить());
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПроверкаДоступностиВалютыУчета();
	Этаформа.Модифицированность = Ложь;
	Элементы.ВалютаУправленческогоУчета.ТолькоПросмотр=Ложь; //*****
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Ошибки="";
	Если НЕ ЗначениеЗаполнено(НаборКонстант.ПрефиксШтучногоТовара) Тогда
		Ошибки = " - Не заполнено значение префикса штрихкода штучного товара."
	Иначе
		Код = КодСимвола(НаборКонстант.ПрефиксШтучногоТовара);
		Если (Код < 48) Или (Код > 57) Тогда
			Ошибки = " - Значение префикса штрихкода штучного товара может принимать значения от 0 до 9";
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НаборКонстант.ПрефиксВесовогоТовара) Тогда
		Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
		"",
		"
		|")
		+ " - Не заполнено значение префикса штрихкода весового товара."
	Иначе
		Код = КодСимвола(НаборКонстант.ПрефиксВесовогоТовара);
		Если (Код < 48) Или (Код > 57) Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
			"",
			"
			|")
			+ " - Значение префикса штрихкода весового товара может принимать значения от 0 до 9.";
		ИначеЕсли НаборКонстант.ПрефиксШтучногоТовара = НаборКонстант.ПрефиксВесовогоТовара Тогда
			Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
			"",
			"
			|")
			+ " - Значение префикса штрихкода весового товара не должно совпадать со значением префикса штрихкода штучного товара.";
		КонецЕсли;
	КонецЕсли;
	
	Если (НаборКонстант.ДлинаКодаВесовогоТовара < 1) Или (НаборКонстант.ДлинаКодаВесовогоТовара > 9) Тогда
		Ошибки = Ошибки + ?(ПустаяСтрока(Ошибки),
		"",
		"
		|")
		+ " - Значение длины кода весового товара может принимать значения от 1 до 9.";
	КонецЕсли;
	
	ИмяСтраницы = "КодыТовара";
	
	Если НЕ ПустаяСтрока(Ошибки) Тогда
		Ошибки = "Были обнаружены следующие ошибки:
		|" + Ошибки;
		Элементы.Страницы.ТекущаяСтраница=Элементы[ИмяСтраницы];
		ПоказатьПредупреждение(,Ошибки); Отказ=Истина;
	КонецЕсли;

	//ПараметрыСеанса.ОрганизацияГрупповогоПроведения=Организация;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Константы.ШаблоныТелефонныхНомеров.Установить(ПолучитьШаблоныНомеров());
КонецПроцедуры
