&НаСервере
Функция НастройкиСтрокойXML(кдСсылка)
	Настройки=кдСсылка.ПолучитьНастройки();
	Если Настройки.Отбор.Элементы.Количество()=0 Тогда Возврат ""; КонецЕсли; 
	Возврат УниверсальныеМеханизмыСервер.ЗначениеВСтрокуXML(Настройки);
КонецФункции

&НаСервере
Процедура НастройкиУсловий_Инициализация(кдСсылка, стрНастройка)
	Если ПустаяСтрока(ТипВидОбъекта) Тогда Возврат; КонецЕсли;
	
	СхемаКомпоновкиДанных=Новый СхемаКомпоновкиДанных;

	ИсточникДанных=СхемаКомпоновкиДанных.ИсточникиДанных.Добавить(); 
	ИсточникДанных.Имя="ИсточникДанных";
	ИсточникДанных.СтрокаСоединения="";
	ИсточникДанных.ТипИсточникаДанных="Local";
	
	НаборДанных=СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя="НаборДанных";
	НаборДанных.ИсточникДанных="ИсточникДанных";
	НаборДанных.АвтоЗаполнениеДоступныхПолей=Истина;
	НаборДанных.Запрос="Выбрать * Из "+ТипВидОбъекта+" Как ИсточникДанных";

	АдресСхемыКомпоновкиДанных=ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	кдСсылка.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));

	Если НЕ ПустаяСтрока(стрНастройка) Тогда
		кдСсылка.ЗагрузитьНастройки(УниверсальныеМеханизмыСервер.ЗначениеИзСтрокиXML(стрНастройка));
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипВидОбъекта()
	Если Не ЗначениеЗаполнено(Запись.Владелец) Тогда ТипВидОбъекта=""; Возврат; КонецЕсли; 
	МассивСтрок=СтрРазделить(Запись.Владелец.ИмяПредопределенныхДанных, "_");
	ТипВидОбъекта=МассивСтрок[0]+"."+МассивСтрок[1];

	//ТипВидОбъекта=?(ЗначениеЗаполнено(Запись.Владелец), СтрЗаменить(Запись.Владелец.ИмяПредопределенныхДанных, "_", "."), "");
КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="Владелец" Тогда
		УстановитьТипВидОбъекта();
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли; 
	
	Если Параметры.Свойство("Владелец") Тогда
		Запись.Владелец=Параметры.Владелец;	
	КонецЕсли;
	УстановитьТипВидОбъекта();

	НастройкиУсловий_Инициализация(кдВидимость, Запись.Видимость);
	НастройкиУсловий_Инициализация(кдОбязателен, Запись.Обязателен);
	НастройкиУсловий_Инициализация(кдДоступность, Запись.Доступность);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.Владелец.Доступность=Запись.Владелец.Пустая();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Видимость=НастройкиСтрокойXML(кдВидимость);
	ТекущийОбъект.Обязателен=НастройкиСтрокойXML(кдОбязателен);
	ТекущийОбъект.Доступность=НастройкиСтрокойXML(кдДоступность);
КонецПроцедуры