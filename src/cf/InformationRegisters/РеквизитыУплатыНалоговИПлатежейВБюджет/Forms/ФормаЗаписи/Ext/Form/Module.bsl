&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.ПоказательТипа.Видимость=ТекущаяДата() < '2015-01-01'; // Приказ Минфина №126н от 30.10.2014.;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиРеквизитовПлатежейВБюджет()
	спБуфер=РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.СтатусыПлательщика();
	Для каждого СтрокаКоллекции Из спБуфер Цикл
		Элементы.СтатусСоставителя.СписокВыбора.Добавить(СтрокаКоллекции.Значение, СтрокаКоллекции.Представление);
	КонецЦикла;

	спБуфер=РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ОснованияПлатежа("Налоговый");
	Для каждого СтрокаКоллекции Из спБуфер Цикл
		Элементы.ПоказательОснования.СписокВыбора.Добавить(СтрокаКоллекции.Значение, СтрокаКоллекции.Представление);
	КонецЦикла;

	спБуфер=РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ТипыПлатежа("Налоговый", ТекущаяДата());
	Для каждого СтрокаКоллекции Из спБуфер Цикл
		Элементы.ПоказательТипа.СписокВыбора.Добавить(СтрокаКоллекции.Значение, СтрокаКоллекции.Представление);
	КонецЦикла;

	спБуфер=РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ВидыНалоговыхПериодов();
	Для каждого СтрокаКоллекции Из спБуфер Цикл
		Элементы.ПоказательПериода.СписокВыбора.Добавить(СтрокаКоллекции.Значение, СтрокаКоллекции.Представление);
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов шапки

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="СтатусСоставителя" Тогда
		Если Не ЗначениеЗаполнено(Запись.СтатусСоставителя) И Элемент.СписокВыбора.Количество() > 0 Тогда
			Запись.СтатусСоставителя=Элемент.СписокВыбора[0].Значение;
		КонецЕсли;
		
	ИначеЕсли Элемент.Имя="ПоказательОснования" Тогда
		Если ПустаяСтрока(Запись.ПоказательОснования) Тогда
			Запись.ПоказательОснования="0";
		КонецЕсли;
		УстановитьВидимость();
		
	ИначеЕсли Элемент.Имя="ПоказательТипа" Тогда
		Если ПустаяСтрока(Запись.ПоказательТипа) Тогда
			Запись.ПоказательТипа="0";
		КонецЕсли;
	КонецЕсли; 	
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_НачалоВыбора(Элемент, СтандартнаяОбработка)
	Если Элемент.Имя="АдминистраторДохода" ИЛИ Элемент.Имя="ВидДохода" Тогда
		ПараметрыФормы=Новый Структура; СтандартнаяОбработка=Ложь;
		ПараметрыФормы.Вставить("ИмяМакета", ?(Элемент.Имя="АдминистраторДохода", "АдминистраторыДоходовБюджетов", "КлассификацияДоходовБюджетов"));
		ОткрытьФорму("РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.Форма.ФормаВыбораКодаИзКлассификатора", ПараметрыФормы, Элемент, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если Элемент.Имя="ВидДохода" Тогда
		СтатьяДоходаДляПоиска=ВидДохода+"0000"+КОСГУ;
		Если ВыбранноеЗначение=СтатьяДоходаДляПоиска Тогда
			СтандартнаяОбработка=Ложь;
		КонецЕсли;
	    ВидДохода=Лев(ВыбранноеЗначение, 10);		
		ПодвидДохода="1000"; // По умолчанию подставляем подвид "Налог" с кодом 1000
		КОСГУ=Сред(ВыбранноеЗначение, 15);
	КонецЕсли;	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Запись.ВидПеречисления) Тогда //Новая запись
		Запись.ПоказательПериода="МС";
		Запись.ОчередностьПлатежа=5;		
		Запись.ВидПеречисления="НалоговыйПлатеж";
	КонецЕсли;
	ЗаполнитьСпискиРеквизитовПлатежейВБюджет();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	АдминистраторДохода=Лев(Запись.КодБК, 3);
	ВидДохода=Сред(Запись.КодБК, 4, 10);
	ПодВидДохода=Сред(Запись.КодБК, 14, 4);
	КОСГУ=Сред(Запись.КодБК, 18);

	УстановитьВидимость();	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Запись.КодБК=АдминистраторДохода+ВидДохода+ПодвидДохода+КОСГУ;
КонецПроцедуры
