&НаКлиенте
Функция ПроверкаПустойСтроки(ВыбСтрока, ПризнакЗапятой=Истина)
	Если ПустаяСтрока(ВыбСтрока) Тогда Возврат ""; КонецЕсли; 
	Возврат ?(ПризнакЗапятой, ",", "")+" ";
КонецФункции

&НаКлиенте
Процедура СформироватьПредставлениеТелефона()
	Если Лев(СокрЛ(Запись.Поле1), 1) <> "+" И НЕ ПустаяСтрока(Запись.Поле1) Тогда
		Запись.Поле1 = СокрЛП(Запись.Поле1);
		Пока Лев(Запись.Поле1, 1) = "0" Цикл
			Запись.Поле1 = Сред(Запись.Поле1, 2);
		КонецЦикла;
		Если НЕ ПустаяСтрока(Запись.Поле1) Тогда
			Запись.Поле1="+"+Запись.Поле1;
		КонецЕсли; 
	КонецЕсли; 

	Запись.Представление=Запись.Поле1;
	Запись.Представление=Запись.Представление+?((Не ПустаяСтрока(Запись.Поле2)), (ПроверкаПустойСтроки(Запись.Представление, Ложь)+"("+Запись.Поле2+")"),"");
	Запись.Представление=Запись.Представление+?((Не ПустаяСтрока(Запись.Поле3)), (ПроверкаПустойСтроки(Запись.Представление, ПустаяСтрока(Запись.Поле2))+Запись.Поле3),"");
	Если НЕ ПустаяСтрока(Запись.Представление) Тогда
		Запись.Представление=Запись.Представление+?((Не ПустаяСтрока(Запись.Поле4)),(ПроверкаПустойСтроки(Запись.Представление)+"доб. "+Запись.Поле4),"");
	Иначе
		Запись.Представление=Запись.Поле4;
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидОбъекта(Ссылка)
	Если Ссылка=Неопределено Тогда
		Возврат Перечисления.ВидыОбъектовКонтактнойИнформации.Контрагенты;
	КонецЕсли;
	Возврат Перечисления.ВидыОбъектовКонтактнойИнформации[Ссылка.Метаданные().Имя];
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов формы

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	СформироватьПредставлениеТелефона();
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элемент.Имя="Представление" Тогда
		Если Запись.Тип=ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
			СтруктураПараметров=Новый Структура;
			СтруктураПараметров.Вставить("Представление","");
			СтруктураПараметров.Вставить("РегионСокращение","");
			СтруктураПараметров.Вставить("РайонСокращение","");
			СтруктураПараметров.Вставить("ГородСокращение","");
			СтруктураПараметров.Вставить("НаселенныйПунктСокращение","");
			СтруктураПараметров.Вставить("УлицаСокращение","");
			СтруктураПараметров.Вставить("Комментарий","");
			СтруктураПараметров.Вставить("УлицаСокращение","");
			СтруктураПараметров.Вставить("ЗначениеПоУмолчанию","");
			СтруктураПараметров.Вставить("ТипДома","");
			СтруктураПараметров.Вставить("ТипКорпуса","");
			СтруктураПараметров.Вставить("ТипКвартиры","");
			СтруктураПараметров.Вставить("КодКладр","");
			СтруктураПараметров.Вставить("Страна","");
			СтруктураПараметров.Вставить("ИдентификаторФИАС","");
			СтруктураПараметров.Вставить("ФИАСДома","");
			
			Для ъ=1 по 10 Цикл
				СтруктураПараметров.Вставить("Поле"+ъ,"");
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(СтруктураПараметров, Запись);

			СтруктураПараметров.Вставить("ЗначенияЗаполнения", Новый Структура("Объект,Тип,Вид", Запись.Объект, Запись.Тип, Запись.Вид));
			
			ОткрытьФорму("РегистрСведений.КонтактнаяИнформация.Форма.ФормаВводаАдреса", СтруктураПараметров, ЭтаФорма, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустой() Тогда
		Параметры.Свойство("Объект", Запись.Объект);
		Параметры.Свойство("Тип", Запись.Тип);
		Если НЕ ЗначениеЗаполнено(Запись.Вид) Тогда
			Запись.Вид=ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаТелефон.Видимость=Запись.Тип=ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон");

	МассивПараметров=Новый Массив();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Тип", Запись.Тип));
	Если Параметры.Свойство("ВидыОбъектов") Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидОбъектаКонтактнойИнформации", Параметры.ВидыОбъектов));
	Иначе
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидОбъектаКонтактнойИнформации", ВидОбъекта(Запись.Объект)));
	КонецЕсли;

	Элементы.Вид.ПараметрыВыбора=Новый ФиксированныйМассив(МассивПараметров);
	Заголовок="Контактная информация ("+СокрЛП(Запись.Объект)+")";
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Запись.Тип=ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		СтруктураПараметров=Новый Структура; Отказ=Истина;
		СтруктураПараметров.Вставить("ЗначенияЗаполнения", Новый Структура("Объект,Тип,Вид", Запись.Объект, Запись.Тип, Запись.Вид));
		ОткрытьФорму("РегистрСведений.КонтактнаяИнформация.Форма.ФормаВводаАдреса", СтруктураПараметров, ЭтаФорма.ВладелецФормы,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
КонецПроцедуры
