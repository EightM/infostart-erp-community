#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ЛьготыНалогаНаИмущество.ЗаголовокСвернутогоОтображения = ЗаголовокГруппыЛьготыНалогаНаИмущество();
	
	Элементы.КодЛьготыОсвобождениеОтНалогаНаИмущество.Доступность = СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения;
	
	Элементы.ОсвобождениеОтНалогообложенияДвижимогоИмущества.Доступность = Не СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения;
	Элементы.СнижениеСтавкиНалогаНаИмущество.Доступность = Не СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения;
	Элементы.СнижениеНалоговойСтавкиДвижимоеИмущество.Доступность = Не СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения;
	
	Элементы.ГруппаУменьшениеСуммыНалогаНаИмущество.Доступность = Не СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения;
	Элементы.ПроцентУменьшенияНалогаНаИмущество.Доступность = СтавкиНалогаНаИмущество.УменьшениеСуммыНалогаВПроцентах;
	
	ВидимостьНалогаНаДвижимоеИмущество = Год(СтавкиНалогаНаИмущество.Период) >= 2018;
	Элементы.ГруппаСтавкаНалогаНаДвижимоеИмущество.Видимость = ВидимостьНалогаНаДвижимоеИмущество;
	Элементы.ОсвобождениеОтНалогообложенияДвижимогоИмущества.Видимость = ВидимостьНалогаНаДвижимоеИмущество;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаголовокГруппыЛьготыНалогаНаИмущество()
	
	
	ЭлементыЗаголовка = Новый Массив;
	
	Если СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'освобождение от налога (%1)'"),
			СтавкиНалогаНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения));
	КонецЕсли;
	
	Если СтавкиНалогаНаИмущество.СнижениеНалоговойСтавки Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'сниженная ставка налога %1%%'"),
			СтавкиНалогаНаИмущество.СниженнаяНалоговаяСтавка));
	КонецЕсли;
	
	Если СтавкиНалогаНаИмущество.УменьшениеСуммыНалогаВПроцентах Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'налог уменьшен на %1%%'"),
			СтавкиНалогаНаИмущество.ПроцентУменьшения));
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Налоговые льготы'");
	Если ЭлементыЗаголовка.Количество() > 0 Тогда
		Заголовок = Заголовок + ": " + СтрСоединить(ЭлементыЗаголовка, ", ");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура("Организация, Период", СтавкиНалогаНаИмущество.Организация, СтавкиНалогаНаИмущество.Период);
	Оповестить("Запись_СтавкиНалогаНаИмущество", ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОсвобождениеОтНалогаНаИмуществоПриИзменении(Элемент) Экспорт
	
	Если СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения Тогда
		СтавкиНалогаНаИмущество.СнижениеНалоговойСтавки = Ложь;
		СтавкиНалогаНаИмущество.СнижениеНалоговойСтавкиДвижимоеИмущество = Ложь;
		СтавкиНалогаНаИмущество.УменьшениеСуммыНалогаВПроцентах = Ложь;
		СтавкиНалогаНаИмущество.ПроцентУменьшения = 0;
		СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества = Ложь;
	Иначе
		СтавкиНалогаНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения = "";
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	ПараметрыФормы.Вставить("НазваниеМакета", "ЛьготыПоНалогуНаИмущество");
	ПараметрыФормы.Вставить("ТекущийПериод", СтавкиНалогаНаИмущество.Период);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбораЗавершение", ЭтотОбъект, ЭтаФорма);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы, ЭтаФорма, , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшениеСуммыНалогаНаИмуществоВПроцентахПриИзменении(Элемент) Экспорт
	
	Если Не СтавкиНалогаНаИмущество.УменьшениеСуммыНалогаВПроцентах Тогда
		СтавкиНалогаНаИмущество.ПроцентУменьшения = 0;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентУменьшенияНалогаНаИмуществоПриИзменении(Элемент) Экспорт
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбораЗавершение(РезультатЗакрытия, Форма) Экспорт
	
	КодЛьготы = РезультатЗакрытия;
	Если КодЛьготы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтавкиНалогаНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения = КодЛьготы;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура СнижениеСтавкиНалогаНаИмуществоПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры
