&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	Если Команда.Имя="Записать" Тогда
		ОбновитьНастройки();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОпределитьТекущегоПользователяГруппуПользователя()
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Вложенный.Пользователь
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь КАК Пользователь,
	|		0 КАК Приоритет
	|	ИЗ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
	|	ГДЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь = &ТекущийПользователь
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗначенияДополнительныхПравПользователя.Пользователь,
	|		1
	|	ИЗ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначенияДополнительныхПравПользователя
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ПользователиГруппы
	|			ПО ПользователиГруппы.Ссылка = ЗначенияДополнительныхПравПользователя.Пользователь
	|				И (ПользователиГруппы.Пользователь = &ТекущийПользователь)) КАК Вложенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вложенный.Приоритет
	|";	
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Пользователь=Выборка.Пользователь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройки()
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда Возврат; КонецЕсли;
	дзБуфер=РеквизитФормыВЗначение("дзДанные");

	НаборЗаписей=РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Использование = Истина;
	НаборЗаписей.Отбор.Пользователь.Значение = Пользователь;
	ЗаполнитьНаборЗаписей(дзБуфер.Строки, НаборЗаписей);
	НаборЗаписей.Записать();
	Модифицированность=Ложь;	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаборЗаписей(СтрокиДерева, НаборЗаписей)
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Не СтрокаДерева.ЭтоГруппа Тогда
			Запись=НаборЗаписей.Добавить();
			Запись.Пользователь=Пользователь;
			Запись.Право=СтрокаДерева.Право;
			Запись.Значение=СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
		Иначе
			ЗаполнитьНаборЗаписей(СтрокаДерева.Строки, НаборЗаписей);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещения_СохранитьДанные(Параметр1, Параметр2) Экспорт
	Если Параметр1=КодВозвратаДиалога.Да Тогда
		ОбновитьНастройки(); Модифицированность=Ложь;	
		Пользователь=Неопределено;
		дзДанные.ПолучитьЭлементы().Очистить();
	КонецЕсли;
	Если Параметр2.ЗакрытьФорму Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры	 

&НаКлиенте
Процедура ПоказтьВопросСохраненияВведенныхДанных(ЗакрытьФорму=Ложь)
	ОписаниеОповещения=Новый ОписаниеОповещения("ОбработчикОповещения_СохранитьДанные", ЭтотОбъект, Новый Структура("ЗакрытьФорму", ЗакрытьФорму));
	ПоказатьВопрос(ОписаниеОповещения, "Данные были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНет, 20,,"Внимание");	
КонецПроцедуры	 
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов формы

&НаКлиенте
Процедура Атрибут_ПриИзменении(Элемент)
	Если Элемент.Имя="Пользователь" Тогда
		дзДанные_Заполнить();
	КонецЕсли;	 
КонецПроцедуры

&НаКлиенте
Процедура Атрибут_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элемент.Имя="Пользователь" Тогда
		Если Модифицированность Тогда
			СтандартнаяОбработка=Ложь;
			ПоказтьВопросСохраненияВведенныхДанных(Ложь);
		КонецЕсли;
	КонецЕсли;	 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий табличного поля "дзДанные"

&НаСервере
Процедура дзДанные_Заполнить()
	дзБуфер=РеквизитФормыВЗначение("дзДанные");
	дзБуфер.Строки.Очистить();

	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Модифицированность=Ложь; Возврат;
	КонецЕсли;
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Родитель,
	|	Ссылка,
	|	ЭтоГруппа,
	|	ЗначениеПрав.Значение
	|ИЗ
	|	ПланВидовХарактеристик.ПраваПользователей КАК Права
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначениеПрав
	|		ПО ЗначениеПрав.Право=Права.Ссылка
	|		И ЗначениеПрав.Пользователь = &Пользователь
	|УПОРЯДОЧИТЬ ПО
	|	Права.ЭтоГруппа ИЕРАРХИЯ,
	|	Права.Наименование
	|";	
	Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не Выборка.Родитель.Пустая() Тогда
			СтрокаГруппы=дзБуфер.Строки.Найти(Выборка.Родитель, "Право", Истина);
			Если СтрокаГруппы=Неопределено Тогда
				СтрокаГруппы=дзБуфер.Строки.Добавить();
				СтрокаГруппы.Право = Выборка.Родитель;
				СтрокаГруппы.ЭтоГруппа = Выборка.ЭтоГруппа;
			КонецЕсли;
		Иначе
			СтрокаГруппы=дзБуфер;
		КонецЕсли;		
		СтрокаПрава=СтрокаГруппы.Строки.Добавить();
		СтрокаПрава.Право=Выборка.Ссылка;
		СтрокаПрава.Значение=Выборка.Ссылка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		СтрокаПрава.ЭтоГруппа=Выборка.ЭтоГруппа;
	КонецЦикла;
	ЗначениеВРеквизитФормы(дзБуфер, "дзДанные");
	
	Модифицированность=Ложь;	
КонецПроцедуры

&НаКлиенте
Процедура дзДанные_ПередНачаломИзменения(Элемент, Отказ)
	Отказ=Элемент.ТекущиеДанные.ЭтоГруппа;
	Если ТипЗнч(Элемент.ТекущиеДанные.Значение)=Тип("Булево") Тогда
		Отказ=Истина; Модифицированность=Истина;
		Элемент.ТекущиеДанные.Значение=НЕ Элемент.ТекущиеДанные.Значение;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий атрибутов

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОпределитьТекущегоПользователяГруппуПользователя();
	ТолькоПросмотр=Не РольДоступна("ПолныеПрава");
	дзДанные_Заполнить();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ЗавершениеРаботы И Модифицированность Тогда
		Отказ=Истина; ПоказтьВопросСохраненияВведенныхДанных(Истина);
	КонецЕсли;	 
КонецПроцедуры
